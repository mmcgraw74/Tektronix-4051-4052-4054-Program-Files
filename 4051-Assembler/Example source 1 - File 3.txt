*  4051 ASSEMBLER EXAMPLE PROGRAM
*
* TO TRY THE ASSEMBLER PREFORM THE FOLLOWING OPERATIONS :
*
*(1)FROM BASIC - PUSH "AUTO LOAD" KEY : THIS LOADS DDT DEBUGGER.
*(2)AFTER DDT PROMPT APPEARS ON SCREEN TYPE "3H" : THIS FINDS MAG
*                                                  TAPE FILE # 3.
*(3)AFTER SYSTEM FINDS FILE # 3 TYPE "}" : THIS STARTS ASSEMBLING
*                                          FROM MAG TAPE FILE.
*(4)AFTER ASSEMBLY IS DONE TYPE "5000;J" : THIS JSR'S TO 5000 HEX
*                                          THUS STARTING PROGRAM
*                                          ASSEMBLED INTO MEMORY.
*(5)TO EXIT PROGRAM EITHER HIT BREAK TWICE OR HIT "CTRL". NOTE
*                                          THAT THIS PARTICULAR
*                                          EXAMPLE PROGRAM IS NOT
*                                          SELF TERMINATING. SOME
*                                          PROGRAMS WILL HAVE A
*                                          "RTS" AT THE END, THUS
*                                          RETURNING TO DEBUGGER.
*
5000      * STARTING ADDRESS
*
ZBOUNCING NOP
          CLR 00A8
          CLR 00A9
          CLR 00AA
          CLR 00AB
          JSR 0CBBF * INIT DISPLAY
          LDX #5800 * INIT POINTER
          LDAA #01 * DO A MOVE
          STAA 00A5
          LDAA 00,X * STARTING X
          STAA 00AA * HI X
          LDAA 08,X
          STAA 00AB * LO X
          LDAA 10,X * STARTING Y
          STAA 00A8 * HI Y
          LDAA 18,X
          STAA 00A9 * LO Y
          JSR 0CB81 * MOVE VECTOR
ZSTART    LDX #5800 * INIT POINT POINTER
          LDAA 87AA * TEST KEYBOARD CONTROL KEY
          ANDA #02
          BNE ZX+DELX * IF NOT ZERO THEN DO DISPLAY
          RTS * ELSE BACK TO THE DEBUGGER
ZX+DELX   LDAA 08,X * X=X+DELTA X
          LDAB 00,X
          TST 30,X * TEST FOR ADD OR SUBTRACT
          BEQ ZXADD * IF ZERO THEN ADD
          SUBA 20,X * ELSE SUBTRACT
          SBCB #00
          BCC ZNOLEFT * IF NO CARRY THEN NOT LEFT WALL
          CLRA * ELSE LEFT WALL
          CLRB
          COM 30,X
ZNOLEFT   STAA 08,X
          STAA 00AB
          STAB 00,X
          STAB 00AA
          JMP ZY+DELY
ZXADD     ADDA 20,X
          ADCB #00
          CMPB #03
          BLE ZNORIGHT * IF <= ZERO THEN NOT RIGHT WALL
          LDAA #FF * ELSE RIGHT WALL
          LDAB #03
          COM 30,X
ZNORIGHT  STAA 08,X
          STAA 00AB
          STAB 00,X
          STAB 00AA
ZY+DELY   LDAA 18,X * Y=Y+DELTA Y
          LDAB 10,X
          TST 38,X * TEST FOR ADD OR SUBTRACT
          BEQ ZYADD * IF ZERO THEN ADD
          SUBA 28,X * ELSE SUBTRACT
          SBCB #00
          BCC ZNOBOTTOM * IF NO CARRY THEN NOT BOTTOM WALL
          CLRA * ELSE BOTTOM WALL
          CLRB
          COM 38,X
ZNOBOTTOM STAA 18,X
          STAA 00A9
          STAB 10,X
          STAB 00A8
          JMP ZDRAW
ZYADD     ADDA 28,X
          ADCB #00
          CMPB #02
          BLE ZNOTOP * IF <= ZERO THEN NOT TOP WALL
          LDAA #FF * ELSE TOP WALL
          LDAB #02
          COM 38,X
ZNOTOP    STAA 18,X
          STAA 00A9
          STAB 10,X
          STAB 00A8
ZDRAW     CLR 00A5 * DO A DRAW
          JSR 0CB81
          INX
          CPX #5808
          BEQ ZSTART
          JMP ZX+DELX
*
*
5800      * TABLE STARTING ADDRESS
*
"02.      * HX0
"03.      * HX1
"00.      * HX2
"03.      * HX3
"02.      * HX4
"00.      * HX5
"03.      * HX6
"00.      * HX7
"00.      * LX0
"0F.      * LX1
"81.      * LX2
"0F.      * LX3
"00.      * LX4
"F1.      * LX5
"7F.      * LX6
"F1.      * LX7
"02.      * HY0
"00.      * HY1
"01.      * HY2
"02.      * HY3
"00.      * HY4
"02.      * HY5
"01.      * HY6
"00.      * HY7
"FE.      * LY0
"70.      * LY1
"7F.      * LY2
"8E.      * LY3
"00.      * LY4
"8E.      * LY5
"7F.      * LY6
"70.      * LY7
"01.      * DELTA X0
"02.      * DELTA X1
"03.      * DELTA X2
"04.      * DELTA X3
"05.      * DELTA X4
"06.      * DELTA X5
"07.      * DELTA X6
"08.      * DELTA X7
"08.      * DELTA Y0
"07.      * DELTA Y1
"06.      * DELTA Y2
"05.      * DELTA Y3
"04.      * DELTA Y4
"03.      * DELTA Y5
"02.      * DELTA Y6
"01.      * DELTA Y7
"00.      * SIGNUM X0
"FF.      * SIGNUM X1
"00.      * SIGNUM X2
"FF.      * SIGNUM X3
"00.      * SIGNUM X4
"FF.      * SIGNUM X5
"00.      * SIGNUM X6
"FF.      * SIGNUM X7
"FF.      * SIGNUM Y0
"00.      * SIGNUM Y1
"FF.      * SIGNUM Y2
"00.      * SIGNUM Y3
"FF.      * SIGNUM Y4
"00.      * SIGNUM Y5
"FF.      * SIGNUM Y6
"00.      * SIGNUM Y7


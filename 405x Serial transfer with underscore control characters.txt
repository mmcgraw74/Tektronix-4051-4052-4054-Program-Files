100 REM 4052 SERIAL COMM TO PC, FROM MCM 4/7/78 4051 TO 8002 SER PGM
110 REM written 12/05/99 mcm V2.0 2/13/00 FOR 4051 AND LONGER STRINGS
120 DIM M$(1),N$(1),B$(1024),C$(8),E$(160)
130 M$=CHR(13)
140 F$=CHR(10)
150 REM SEND DC3/DC1 TO PC ON PAGE FULL AND ALLOW
160 REM USER TO CONTINUE COMMUNICATION BY MANUAL PAGE COMMAND
170 CALL "BREAK",5,"S_","Q_"
182 REM CHECK FOR 4051
183 IF RND(0)<>0.196324846518 THEN 190
184 REM 4051 CAN ONLY DO 2400 BAUD AND NO HW FLOW CONTROL
185 CALL "RATE",2400,2,0
186 GO TO 200
189 REM NEXT STATEMENT ONLY FOR 4052/4054
190 CALL "CMFLAG",3
192 REM SET RATE TO 9600 BAUD for 4052,7 DATA BITS,1STOP BIT, IGNORE
194 CALL "RATE",9600,2,0
200 REM SET DISPLAY FOR 35 LINES OF 74 CHARACTERS
210 CALL "MARGIN",1,0,0
240 REM ALLOW RECEIVED LF AND SUPPRESS CONTROL CHARACTERS FOR DISPLAY
250 CALL "RCRLF",1,2,0
260 REM DELIMIT RECEIVED LINES ON CR AND CLOSE FILE AFTER*DONE*;RECEIVD
270 CALL "RSTRIN","",M$,"*DONE*"
280 REM SET NO LOCAL LINE FEED ON TAPE SEND AND DON`T MODIFY CR OR LF
290 CALL "TCRLF",1,2,1
300 PRINT "L_ESTABLISH COMMUNICATION LINK to PC"
310 PRINT "__Press User Definable Key #5 to switch back to BASIC__"
320 REM SET THE ABOVE COMM PARAMETERS AND ENTER THE TERMINAL MODE
330 CALL "CMSET"
340 CALL "TERMIN"
350 REM YOU HAVE EXITED THE TERMINAL MODE
360 PRINT "YOU ARE NOW IN BASIC MODE"
370 PRINT "DO YOU WANT 4052 TAPE TO PC DISC TRANSFER? (1)"
380 PRINT "         OR PC DISC TO 4052 TAPE TRANSFER? (2)";
390 INPUT N
400 REM SET THE DISPLAY FOR AUTO PAGE WHEN FULL
410 PRINT @32,26:2
420 GO TO N OF 430,990
430 PRINT "L_INSERT TAPE AND INPUT FILE # TO BE TRANSMITTED TO PC ";
440 INPUT F
450 FIND F
460 REM WATCH FOR EOF ON TAPE DURING TRANSMISSIONON, THEN BRANCH EXIT
470 ON EOF (0) THEN 940
480 REM SEND CR TO PC TO BEGIN TRANSFER THEN DELAY UNTIL FILE IS OPEN
490 REM DONT DO THIS, CREATES TWO CR AT FILE BEGINING ---PRINT @40:M$
500 FOR I=1 TO 2
510 NEXT I
520 REM COUNT THE NUMBER OF LINES TRANSMITTED (L)
530 L=0
540 REM LOOP UNTIL 1000 LINES ARE READ FROM TAPE AND TRANSMITTED
550 FOR I=1 TO 1000
560 INPUT @33:B$
570 E$=B$
575 F$=CHR(8)
580 REM   REPLACE CONTROL CHARACTERS WITH X BS_ WHERE X=DEC VALUE CTL
590 A=LEN(B$)
600 IF A=0 THEN 800
610 L=1
620 M=A
630 FOR K=1 TO A
640 C$=SEG(B$,K,1)
645 D=ASC(C$)
650 IF D=>32 THEN 780
660 D$=CHR(D+64)
670 D$=D$&F$
680 D$=D$&"_"
730 E$=REP(D$,L,1)
740 REM L AND M ARE BUMPED UP BY THREE TO ACCOUNT FOR THE LONGER STR
750 L=L+4
760 M=M+4
770 NEXT K
780 L=L+1
790 NEXT K
800 REM DELAY TO ALLOW PC TIME TO PROCESS THE LINE
810 B$=E$
820 FOR J=1 TO 2
830 NEXT J
840 REM DISPLAY LINE TO BE TRANSMITTED THEN TRANSMIT
850 PRINT B$
860 PRINT @40:B$;M$;F$;
870 NEXT I
880 L=L+1000
890 PRINT "L_",L," LINES TRANSMITTED, TAPE FILE EXCEEDS ",L," LINES."
900 PRINT "DO YOU WANT TO CONTINUE?"
910 INPUT N$
920 IF N$="Y" THEN 550
930 GO TO 960
940 REM ON EOF, SEND CONTROL Z TO PC TO CLOSE THE DISC FILE
950 PRINT "EOF CONDITION, ";L+I-1;" LINES TRANSMITTED.__"
960 PRINT "YOU ARE NOW IN THE TERMINAL MODE.__"
970 PRINT @40:"Z_"
980 GO TO 340
990 REM PC DISK TO 4052 TRANSFER
1000 PRINT "INSERT THE TAPE AND INPUT THE FILE # TO RECEIVE DATA ";
1010 INPUT F
1020 FIND F
1030 REM SEND CR TO INITIATE PC TRANSMISSION
1040 PRINT @40:M$
1050 REM SUPPRESS THE EXTRA LINE FEED FROM THE PC
1060 REM -- INPUT @40:B$
1070 REM MODIFY CONTROL CHARACTERS FOR DISPLAY
1080 CALL "RCRLF",1,2,1
1090 CALL "CMSET"
1100 REM SWITCH TO THE TAPE DATA RECEIVE MODE
1110 CALL "DTRECV"
1120 REM SUPPRESS CONTROL CHARACTERS FOR DISPLAY
1130 CALL "RCRLF",1,2,0
1140 PRINT "__TRANSMISSION COMPLETE__"
1150 GO TO 330
1160 REM  TEST A_ CONTROL A SHOULD TRANSFER AS 01 WITH SURROUNDING ~


4 GO TO 100
24 GOSUB 510
40 PAGE 
41 GOSUB 610
100 PAGE 
110 PRINT "************* WELCOME TO THE CU-SUM PROGRAM *************"
120 PRINT 
130 PRINT "    USE IT TO SEE HOW YOUR VEHICLE PERFORMS."
140 PRINT 
150 PRINT "  YOU WILL BE ASKED TO KEY IN THE NUMBER OF PAIRS OF VALUES"
160 PRINT 
170 PRINT " YOU HAVE FOR (a) GALLONS (b) MILEOMETER, THEN PROMPTED FOR"
180 PRINT 
190 PRI " EACH VALUE IN TURN. AFTER INPUT YOU WILL HAVE AN OPPORTUNITY"
200 PRINT 
210 PRINT " TO CHECK THE DATA BEFORE DISPLAYING THE GRAPH."
220 PRINT 
230 PRINT 
240 PRINT "  AFTER DATA VERIFICATION YOU WILL BE ASKED TO KEY THE"
250 PRINT 
260 PRINT "  EXPECTED MILES PER GALLON."
270 PRINT 
280 PRINT 
290 PRINT "   PLEASE HAVE YOUR FIGURES FOR GALLONS AND MILEAGE ";
300 PRINT "READY TO INPUT - - -"
310 PRINT 
320 PRINT " BUT, FIRST OF ALL, HOW MANY PAIRS OF READINGS DO YOU HAVE ?"
330 PRINT 
340 INPUT N
350 DELETE S,G,Q,A,T
360 DIM S(N),G(N),Q(N),A(N),T(N)
370 REM
380 REM X7 CLEARS SCREEN AFTER 5 PAIRS ENTERED
390 REM
400 X7=0
410 FOR I=1 TO N
420 IF X7<>5 THEN 450
430 X7=0
440 PAGE 
450 PRINT " NO. OF GALLONS ?"
460 INPUT G(I)
470 PRINT " SPEEDO READING ?"
480 INPUT S(I)
490 X7=X7+1
500 NEXT I
510 PAGE 
520 PRINT USING 530:"    GALLONS  ";"    MILEAGE  "
530 IMAGE 10T,13A,25T,13A
540 IMAGE10T,2A,13T,FD,15T,2A,17T,FD.2D,26T,2A,28T,FD,30T,2A,32T,FD
545 PRINT 
546 PRINT USING 530:"-------------";"-------------"
550 FOR I=1 TO N
560 PRINT USING 540:"G(";I;")=";G(I);"S(";I;")=";S(I)
570 NEXT I
580 PRINT " Are the data correct ?";"Key 'Y' or 'N'";
581 INPUT J$
582 IF J$<>"N" THEN 590
585 GOSUB 2000
590 REM
591 REM ANYTHING OTHER THAN 'N' IS = YES
592 REM
600 REM
610 REM SOUND AN ALARM NOW TO INDICATE THE LAST ENTRY HAS BEEN MADE
620 REM
630 PRINT ""
640 PRINT " WHAT IS THE STANDARD ? i.e. The expected miles per gallon ?"
650 INPUT E
660 FOR I=1 TO N
670 IF I=>2 THEN 760
680 REM
690 REM A= ANTICIPATED MILES i.e. STD* GALLONS BOUGHT
700 REM
710 A(I)=G(I)*E
720 T=0
730 T(1)=G(1)
740 Q(I)=0
750 GO TO 850
760 A(I)=G(I)*E
770 T(I)=T(I-1)+G(I)
780 REM
790 REM T= CUSUM OF GALLS ; Q=CUSUM MILES
800 REM
810 Q(I)=S(I)-S(I-1)
820 Q(I)=Q(I)-A(I-1)
830 Q(I)=Q(I)+Q(I-1)
840 Q(I-1)=Q(I)
850 NEXT I
860 PAGE 
870 VIEWPORT 10,120,10,100
880 WINDOW 0,T(N)+100,-400,400
890 AXIS 50,100,0,0
900 REM
910 REM    START LABELLING THE AXIS
920 REM
930 DELETE K$
940 DIM K$(5)
950 FOR I=-400 TO 400 STEP 100
960 MOVE 0,I
970 PRINT "";
980 K$=STR(I)
990 PRINT K$;
1000 NEXT I
1010 DIM P$(20)
1020 P$="CUMULATIVE GALLONS"
1030 MOVE 0,0
1040 FOR I=1 TO N
1050 RDRAW T(I),Q(I)
1060 NEXT I
1070 MOVE 10,0
1080 PRINT P$;
1090 DELETE P$
1100 DIM Q$(15),R$(1)
1110 Q$="CU-SUM (MILES)"
1120 MOVE 0,150
1130 PRINT "";
1140 FOR I=1 TO LEN(Q$)
1150 R$=SEG(Q$,I,1)
1160 PRINT R$;"
";
1170 NEXT I
1180 DELETE K$
1190 DIM K$(4)
1200 FOR I=100 TO T(N)+100 STEP 100
1210 MOVE I,0
1220 PRINT "
";
1230 K$=STR(I)
1240 PRINT K$
1250 NEXT I
1260 M=(S(N)-S(1))/(T(N)-G(N))
1270 MOVE 10,400
1280 IMAGE 18A,FD.2D,25T,16A,FD.2D
1290 IMAGE 23T,15A,FD
1300 PRINT USING 1280:"TOTAL GALS BOUGHT=";T(N);"  MILES PER GAL=";M;
1310 PRINT USING 1290:"TOTAL MILEAGE =";S(N)-S(1)
1320 MOVE 10,-400
1330 PRINT "STANDARD M. P. G. USED ";E;
1340 PRINT "";
1350 PRINT " IF YOU WISH TO TRY ANOTHER STANDARD, KEY UDK 10";
1360 END 
2000 REMSUBROUTINE CHANGE DATA
2005 DELETE J$
2010 PRINT 
2020 PRINT "CHANGE A DATA ITEM BY NOTING ITS PLACE IN THE ARRAY -"
2030 PRINT " e.g. G(9) or S(15)    THEN CHANGE THE VALUE BY KEYING"
2040 PRINT "    'G(9)=7.33' or 'S(15)=4366'"
2041 PRINT 
2042 PRINT 
2043 PRINT " When ready to continue press User Definable Key 6."
2050 END 


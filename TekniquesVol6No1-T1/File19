1 REM Label Printing Program
2 SET KEY 
3 GO TO 100
4 REM              HELP
5 GO TO 5640
6 GO TO 6
8 REM              ENTER
9 GO TO 930
12 REM SAVE
13 GO TO 6120
16 REM            PRINT LABELS
17 GO TO 2160
20 REM            DISPLAY LABEL FOR EDITING
21 GO TO 3490
24 REM            EXPAND
25 GO TO 5020
28 REM            BACKSPACE
29 GOSUB 4600
30 GO TO 4470
32 REM            SPACE
33 GOSUB 4680
34 GO TO 4470
36 REM            CLEAR EDIT LINE
37 GO TO 4930
40 REM            SELECT LINE FOR EDITING
41 GO TO 3680
44 REM            LIST LABEL
45 GO TO 6530
48 REM            FILLIN LABEL
49 GO TO 2050
52 REM            OLD
53 GO TO 6340
56 REM            CHANGE SEQUENCING
57 GO TO 2780
60 REM            RESTORE LINE TO LABEL
61 GO TO 3920
64 REM            COMPRESS
65 GO TO 5120
68 REM            RUBOUT LEFT
69 GO TO 4790
72 REM            RUBOUT RIGHT
73 GO TO 4860
76 REM            REPRINT
77 GOSUB 5270
80 REM            INPUT CHARACTER
81 GO TO 5330
89 STOP 
90 REM
91 REM WHEN A LABEL IS CREATED, IT IS STORED IN A$ AND D$
92 REM WHEN A LABEL IS PRINTED IT IS COPIED TO D$ AND MAY BE FILLED.
93 REM \\\'S CAN BE EDITED TO ANYTHING BEFORE PRINTING.
94 REM DURING FILLING A LABEL IS MOVED FROM A$ TO D$
95 REM---------------------------------------------------------
100 REM
102 REM           ----- SYMBOL TABLE -----
104 REM
110 REM A$  : STORAGE FOR UNFILLED LABEL
120 REM B$  : TEMPORARY INPUT STORAGE B(200)
130 REM C$  : CARRAGE RETURN SYMBOL
140 REM C1  : BEGINNING OF UNFILLED PORTION OF LABEL
150 REM C2  : START OF \\\'S
160 REM C8  : BEGINING OF UNPRINTED PORTION OF FILL OUT STRING
170 REM C9  : END OF LINE CONTAINING \\\'S
180 REM D$  : STRING FOR FILLING OR EDITING
190 REM D1  : PRINTER ADDRESS.  LEFT SLOT IS 41
200 REM E$  : SYMBOL FOR FILL OUT AREA (\\\'S)
210 REM E1  : HIDDEN HELP MESSAGE CODE
220 REM F$  : CONTAINS LINE BEING EDITED
230 REM G$  : ARRAY OF BLANKS FOR PADDING
240 REM H   : HEIGHT IN GDU'S OF LINE
250 REM H$  : TEMPORARY STORE FOR INPUT RESPONSE
260 REM H1  : LOCATION OF 1ST LINE
270 REM I   : INDEX
280 REM I$  : TEMP PAD FOR EDIT INPUT
290 REM I1  : TEMP INDEX
300 REM I2  : TEMP INDEX
310 REM I3  : NUMBER OF LABLES TO BE PRINTED
320 REM I4  : TEMP INDEX IN PRINTING
330 REM J   : TEMP VAR IN LABEL OLD
340 REM J$  : REFRESHED CHARACTER USED AS POINTER IN EDITING
350 REM L1  : NUM OF LINES IN LABEL IN ENTRY OR NUM OF \'S IN FILLING
360 REM N1  : 0 OR LOCATION OF %'S FOR SEQUENCING
370 REM N2  : LENGTH OF STRING OF %'S
380 REM N3  : STARTING VALUE OF SEQUENCE (DEFAULT 1)
390 REM N4  : INCREMENT OF SEQUENCING (DEFAULT 1)
400 REM N5  : NUMBER OF REPEATS IN LABEL PRINTING
410 REM N6  : COUNT DOWN COUNTER FOR REPEAT LOOP
420 REM N7  : SEQUENCE NUMBER
430 REM P1  : X COORDINATE OF ALPHA CURSOR
440 REM P2  : Y     "          "       "
450 REM S1  : FILE NUMBER OF 1ST LABEL ON TAPE
460 REM S2  : FILE NUMBER OF LABEL ON TAPE
470 REM S3  : FILE NUMBER OF LAST LABEL ON TAPE
480 REM S9  : LENGTH OF LABEL BUFFERS (A$,D$)
490 REM T   : TABLE OF LINE BEGININGS AND LENGTHS  T(20,2)
500 REM T1  : NUMBER OF LINES IN TABLE INCLUDING 2 EXTRA
510 REM T2  : LOCATION OF CR AT END OF LINE WHILE BUILDING TABLE
520 REM T3  : LENGTH OF D$ DURING EDITING, INDEX WHEN DISPLAYING.
530 REM T4  : TEMP VARI OR LINE BEING EDITED
540 REM T5  : POSITION OF POINTER IN EDIT LINE
550 REM T6  : LENGTH OF EDIT LINE
560 REM T7  : TEMP VARIABLE
570 REM W   : WIDTH IN GDUS OF 1 CHARACTER
580 REM W1  : LOCATION OF FIRST CHARACTER IN EDIT LINE
590 REM Z   : DEBUG VARIABLE
600 REM Z$  : DEBUG STRING
610 REM
620 REM         ----- START OF PROGRAMME -----
630 INIT 
640 PAGE 
650 SET KEY 
660 PRINT "PRESS #1 FUNCTION KEY FOR HELP"
670 DIM A$(1000)
680 DIM B$(200)
690 DIM D$(1000)
700 S9=1000
710 C$=CHR(13)
720 A$=SEG(C$,1,0)
730 D$=A$
740 F$=A$
750 G$="                                                               "
760 REM IT IS ASSUMED THAT THE PRINTER IS IN THE LEFT SLOT @41:
770 D1=41
780 E1=1
790 REM INITIALIZE SEQUENCING DEFAULTS
800 REM N3:START,N4:STEP,N5:REPEAT,N6:REPEAT COUNT DOWN
810 N1=0
820 N3=1
830 N4=1
840 N5=1
850 N6=N5
860 REM S1,S3  FIRST AND LAST FILE NUMBER OF STORED LABELS
870 S1=3
880 S3=25
890 REM INIT EDITOR
900 GOSUB 3120
910 REM---------------------------------------------------------
920 REM ENTER NEW LABEL
930 IF LEN(A$)=0 THEN 1010
940 PRINT "OLD LABEL EXISTS.  ENTER CR TO CLEAR IT OR"
950 PRINT "SELECT ANOTHER FUNCTION KEY"
960 INPUT B$
970 REM DEFINE A$ AS 0 LENGTH STRING
980 A$=SEG(C$,1,0)
990 D$=A$
1000 F$=A$
1010 PRINT "HOW MANY HOLES PER LABEL ARE THERE? ";
1020 INPUT L1
1030 L1=L1*3
1040 FOR I1=1 TO L1
1050 PRINT USING "FA,3D,XS":"LINE",I1
1060 INPUT B$
1070 IF LEN(B$)<1 THEN 1160
1080 IF LEN(B$)+LEN(A$)<S9-20 THEN 1110
1090 PRINT "MAX LABEL LENGTH EXCEEDED"
1100 GO TO 1160
1110 REM CONCATANATE LINES SEPERATED BY CR
1120 B$=B$&C$
1130 A$=A$&B$
1140 NEXT I1
1150 IF I1=>L1 THEN 1210
1160 REM PAD TO FULL NUMBER OF LINES FOR 1 LABEL
1170 FOR I2=I1 TO L1
1180 A$=A$&C$
1190 NEXT I2
1200 REM LABLE ASSEMBLED AND STORED IN D$ AND IN A$
1210 D$=A$
1220 REM CLEAR EDIT TABLE
1230 T1=0
1240 PRINT "LABEL",C$,A$,C$,"PRESS #1 FOR HELP"
1250 REM
1260 GO TO 1250
1270 REM---------------------------------------------------------
1280 REM SUBROUTINE TO PRINT AND FILLOUT STRING D$
1290 REM C1 IS BEGINING OF UNSEARCHED SECTION
1300 REM FIND STRING OF \\\'S
1310 GOSUB 1580
1320 B$=SEG(D$,C8+1,C9-C8)
1330 PRINT B$
1340 REM EXIT IF NO \\\'S
1350 IF C2<1 THEN 1500
1360 PRINT "FILLOUT-";
1370 INPUT B$
1380 REM APPEND BLANKS FOR SHORT STRING
1390 B$=B$&G$
1400 B$=SEG(B$,1,L1)
1410 D$=REP(B$,C2,L1)
1420 B$=SEG(D$,C8+1,C9-C8)
1430 PRINT B$
1440 PRINT "PRESS RETURN IF OK ";
1450 INPUT B$
1460 IF LEN(B$)>0 THEN 1360
1470 REM STRING REPLACED PROPERALY
1480 C1=I1
1490 GO TO 1310
1500 REM CHECK TO SEE IF THERE IS ANY UNPRINTED STRING
1510 IF C9=>LEN(D$) THEN 1550
1520 REM PRINT BALANCE OF STRING
1530 B$=SEG(D$,C9+1,LEN(D$)-C9)
1540 PRINT B$
1550 RETURN 
1560 REM END OF FILLOUT
1570 REM---------------------------------------------------------
1580 REM SUBROUTINE TO FIND STRING OF E$'S
1590 REM D$ IS A MULTILINE SET OF CHARACTERS WITH IMBEDDED CR DELIMITING
1600 REM    LINES AND STRINGS OF E$(\\\\)'S
1610 REM C1 IS BEGINING OF SEARCH
1620 REM C2 IS BEGINING OF STRING OF \\\'S (0 IF NO \\\'S)
1630 REM L1 IS LENGTH OF STRING
1640 REM I1 IS END OF STRING+1 UNLESS THAT WOULD BE A CR
1650 REM C8 IS BEGINING OF CURRENT LINE (MAY BE < C1)
1660 REM C9 IS END OF CURRENT LINE
1670 REM
1680 E$="\"
1690 C2=0
1700 L1=0
1710 C2=POS(D$,E$,C1)
1720 REM NO STRING TO SUBSTITUE IF C2=0
1730 IF C2<1 THEN 2010
1740 REM LOCATE END OF LINE(NEED TO FIND ALL STRINGS IN ONE LINE
1750 C9=POS(D$,C$,C2)
1760 FOR I1=C2+1 TO C9
1770 REM ?
1780 F$=SEG(D$,I1,1)
1790 IF F$<>E$ THEN 1820
1800 NEXT I1
1810 REM LOOP ALWAYS FAILS ON TEST
1820 L1=I1-C2
1830 REM
1840 REM IF AT END OF LINE RETURN
1850 IF F$<>C$ THEN 1880
1860 REM INCREMENT I1 AROUND CR
1870 I1=I1+1
1880 REM BRANCH IF STILL IN CURRENT LINE
1890 REM IF THE NEXT LINE IS DELETED, EDITING WILL GO FASTER BECAUSE
1900 REM    ONLY THE EDITED LINE WILL BE PRINTED. THIS IS NOT GOOD FOR
1910 REM    SHORT LABELS.
1920 GO TO 2010
1930 REM FIND PREVIOUS CARRIGE RETURN
1940 C8=C2
1950 C8=C8-1
1960 IF C8=0 THEN 2000
1970 H$=SEG(D$,C8,1)
1980 IF H$=C$ THEN 2000
1990 GO TO 1950
2000 C1=C8
2010 RETURN 
2020 REM
2030 REM----------------------------------------------------
2040 REM
2050 REM PROCEDURE TO FILL OUT LABEL.
2060 PRINT "FILL OUT LABEL"
2070 D$=A$
2080 T1=0
2090 C1=1
2100 C8=0
2110 C9=POS(D$,C$,1)
2120 GOSUB 1280
2130 PRINT "FILLOUT COMPLETE"
2140 PRINT 
2150 REM---------------------------------------------------
2160 REM START PRINTING LABELS
2170 E1=4
2180 IF LEN(D$)>0 THEN 2250
2190 E1=4
2200 IF LEN(A$)<1 THEN 5600
2210 D$=A$
2220 N1=0
2230 T4=0
2240 E1=1
2250 IF E1=6 THEN 2290
2260 E1=5
2270 H$=" "&C$
2280 IF T4>0 AND F$<>H$ THEN 5600
2290 REM CHECK BY PRINTING OUT SAMPLE
2300 E1=1
2310 PRINT "IS THIS WHAT YOU WANT?"
2320 PRINT D$
2330 INPUT B$
2340 IF B$="NO" THEN 2560
2350 IF B$="N" THEN 2560
2360 REM ACTUAL LABEL MAKING
2370 PRINT "PRINT SMALL NUMBERS TO CHECK ALIGNMENT"
2380 PRINT "MOVE PAPER UP OR DOWN WITH KNOB AT LEFT"
2390 PRINT "HOW MANY LABELS? ";
2400 I3=0
2410 INPUT I3
2420 REM QUIT ON 0 LABELS
2430 IF I3=0 THEN 2570
2440 REM CHECK FOR SEQUENCING
2450 N7=N3
2460 REM LOCATE %%%'S
2470 GOSUB 2600
2480 REM
2490 FOR I4=1 TO I3
2500 REM CHECK FOR INCREMENTING
2510 GOSUB 2970
2520 PRINT @D1:D$;
2530 NEXT I4
2540 REM REPEAT
2550 GO TO 2390
2560 PRINT "PRESS KEY #1 FOR HELP"
2570 PRINT "STOP"
2580 WAIT 
2590 REM---------------------------------------------
2600 REM SUBROUTINE TO LOCATE %%%'S AND SET UP FOR SEQUENCING
2610 REM N1= 0 OR 1ST % STRING, N2=LEN OF %, I4,N3,N4,F$ IS LOCAL
2620 REM
2630 IF N1>0 THEN 2720
2640 N1=POS(D$,"%",1)
2650 REM EXIT IF NO %'S
2660 IF N1<1 THEN 2760
2670 FOR I4=N1 TO LEN(D$)
2680 F$=SEG(D$,I4,1)
2690 IF F$<>"%" THEN 2710
2700 NEXT I4
2710 N2=I4-N1
2720 IF N4<>0 THEN 2760
2730 REM CLEAR %%%'S
2740 F$=SEG(G$,1,N2)
2750 D$=REP(F$,N1,N2)
2760 RETURN 
2770 REM--------------------------------------------------
2780 REM SUBROUTINE TO ALTER SEQUENCING DEFAULTS
2790 PRINT "ENTER STARTING VALUE. (DEFAULT IS  ";N3;") ";
2800 INPUT B$
2810 IF LEN(B$)<1 THEN 2830
2820 N3=VAL(B$)
2830 PRINT "ENTER STEP VALUE. (DEFAULT IS  ";N4;") ";
2840 INPUT B$
2850 IF LEN(B$)<1 THEN 2880
2860 N4=VAL(B$)
2870 IF N4=0 THEN 2930
2880 PRINT "YOU CAN HAVE REPEATS OF EACH LABEL BEFORE STEPPING."
2890 PRINT "HOW MANY OF EACH? (DEFAULT IS  ";N5;") ";
2900 INPUT B$
2910 IF LEN(B$)<1 THEN 2930
2920 N5=VAL(B$)
2930 RETURN 
2940 REM
2950 REM
2960 REM-----------------------------------------
2970 REM SUBROUTINE TO UPDATE INDEX ON LABEL
2980 REM ASSUMES N1=0 OR LOC, N2=LEN, N3=INDEX, N4=INCREMENT
2990 REM F$ IS LOCAL OUTPUT IS D$
3000 IF N1<1 OR N4=0 THEN 3090
3010 N6=N6-1
3020 IF N6>0 THEN 3090
3030 N6=N5
3040 F$=STR(N7)
3050 N7=N7+N4
3060 F$="          "&F$
3070 F$=SEG(F$,LEN(F$)-N2+1,N2)
3080 D$=REP(F$,N1,N2)
3090 RETURN 
3100 REM -----------------------------------------------
3110 REM EDIT LABLE IN STRING D$
3120 REM INITIALIZATION
3130 REM T(20),2):TABLE OF LINE,T1:# OF LINES
3140 DIM T(20,2)
3150 T1=0
3160 T4=0
3170 H1=100
3180 W1=9
3190 H=3
3200 W=1.8
3210 P1=W1
3220 P2=H1
3230 F$=SEG(C$,1,0)
3240 RETURN 
3250 REM BUILD LINE TABLE
3260 T3=LEN(D$)
3270 IF T3>0 THEN 3300
3280 D$=A$
3290 T3=LEN(D$)
3300 T(1,1)=1
3310 T(1,2)=0
3320 T1=1
3330 T2=0
3340 T2=T2+1
3350 IF T2>T3 THEN 3430
3360 B$=SEG(D$,T2,1)
3370 IF B$<>C$ THEN 3340
3380 T(T1,2)=T2-T(T1,1)+1
3390 T1=T1+1
3400 T(T1,1)=T2+1
3410 GO TO 3340
3420 REM
3430 REM END OF STRING REACHED, CR ASSUMED
3440 T(T1,2)=0
3450 T1=T1-1
3460 REM END OF TABLE BUILDING
3470 RETURN 
3480 REM
3490 REM DISPLAY LABEL AS PER TABLE
3500 REM CHECK FOR TABLE BUILT
3510 IF T1>1 THEN 3530
3520 GOSUB 3250
3530 PAGE 
3540 P2=H1
3550 FOR T3=1 TO T1
3560 REM MOVE DOWN 1 LINE
3570 GOSUB 4350
3580 B$=SEG(D$,T(T3,1),T(T3,2))
3590 T7=T3*10
3600 PRINT USING "4D,1X,68A  ":T7,B$
3610 NEXT T3
3620 REM CHECK FOR EDIT LINE
3630 IF LEN(F$)<1 THEN 3650
3640 GOSUB 5270
3650 RETURN 
3660 END 
3670 REM---------------------------------------------------
3680 REM SUBROUTINE TO EXTRACT LINE INTO F$ FOR EDITING
3690 IF T1>1 THEN 3710
3700 GOSUB 3250
3710 GOSUB 4350
3720 PRINT "ENTER LINE NUMBER ";
3730 INPUT T4
3740 T4=T4/10
3750 T8=T4-INT(T4)
3760 IF T8<0.01 THEN 3800
3770 REM NEW LINE
3780 F$=C$
3790 GO TO 3810
3800 F$=SEG(D$,T(T4,1),T(T4,2))
3810 GOSUB 4350
3820 IF F$<>C$ THEN 3840
3830 F$=" "&F$
3840 P1=W1
3850 T5=1
3860 T6=LEN(F$)
3870 GOSUB 4420
3880 GO TO 4560
3890 STOP 
3900 REM
3910 REM---------------------------------------------------
3920 REM RETURN LINE
3930 E1=8
3940 IF T4<0.01 THEN 5600
3950 REM DELETE TRAILING BLANKS
3960 I=LEN(F$)
3970 I=I-1
3980 IF I=0 THEN 4030
3990 H$=SEG(F$,I,1)
4000 IF H$<>" " THEN 4030
4010 F$=REP(C$,I,2)
4020 GO TO 3970
4030 REM CHECK FOR LABEL GETTING TOO LONG
4040 E1=7
4050 REM CHECK FOR LINE ADDED TO ENDS OF TABLE
4060 L1=0
4070 IF T4<1 OR T4>T1 THEN 4090
4080 L1=T(T4,2)
4090 IF LEN(D$)-L1+LEN(F$)>S9-20 THEN 5600
4100 REM IF EDITED STRING IS EMPTY, THEN DELETE LINE
4110 IF LEN(F$)>1 THEN 4130
4120 F$=SEG(G$,1,0)
4130 REM CHECK FOR LINE ADDED ON END OF LABLE
4140 IF T4<=T1 THEN 4160
4150 T4=T1+0.5
4160 T8=T4-INT(T4)
4170 IF T8<0.01 THEN 4260
4180 REM INSERT LINE
4190 IF T4=>1 THEN 4240
4200 REM INSERT LINE AT BEGINING
4210 D$=F$&D$
4220 GO TO 4270
4230 REM INSERT LINE AFTER T4
4240 D$=REP(F$,T(INT(T4+1),1),0)
4250 GO TO 4270
4260 D$=REP(F$,T(T4,1),T(T4,2))
4270 T(T4,2)=LEN(F$)
4280 T1=0
4290 F$=SEG(C$,1,0)
4300 T4=0
4310 REM E1=1
4320 RETURN 
4330 REM
4340 REM---------------------------------------------------
4350 REM SUBROUTINE TO MOVE PRINT POINTER DOWN
4360 P2=P2-H
4370 MOVE P1,P2
4380 RETURN 
4390 REM
4400 STOP 
4410 REM---------------------------------------------------
4420 REM ROUTINE TO PRINT LINE AND POINTER
4430 GOSUB 5540
4440 MOVE 0,P2
4450 T7=T4*10
4460 PRINT USING "4D,1X,72A":T7,F$;
4470 REM ENTRY TO PRINT POINTER
4480 GOSUB 5540
4490 E1=1
4500 J$=SEG(F$,T5,1)
4510 IF J$<>" " THEN 4530
4520 J$="_"
4530 MOVE P1,P2
4540 RETURN 
4550 REM WAIT LOOP WHILE EDITING----------------------------
4560 PRINT @32,24:J$,"";
4570 IF T4<0.01 THEN 4590
4580 GO TO 4560
4590 RETURN 
4600 REM MOVE POINTER LEFT
4610 GOSUB 5540
4620 IF T5<=1 THEN 4660
4630 T5=T5-1
4640 P1=P1-W
4650 GOSUB 5270
4660 RETURN 
4670 REM END
4680 REM MOVE POINTER RIGHT
4690 GOSUB 5540
4700 IF T5<T6-1 THEN 4750
4710 REM lengthen line
4720 IF T6=>69 THEN 4770
4730 F$=REP(" ",T6,0)
4740 T6=T6+1
4750 T5=T5+1
4760 P1=P1+W
4770 RETURN 
4780 REM
4790 REM RUBOUT LEFT
4800 GOSUB 5540
4810 J$=SEG(F$,T5,1)
4820 IF J$<>" " THEN 4840
4830 GOSUB 4600
4840 F$=REP(" ",T5,1)
4850 GO TO 4470
4860 REM RUBOUT RIGHT
4870 GOSUB 5540
4880 J$=SEG(F$,T5,1)
4890 IF J$<>" " THEN 4910
4900 GOSUB 4680
4910 F$=REP(" ",T5,1)
4920 GO TO 4470
4930 REM CLEAR
4940 GOSUB 5540
4950 F$=" "&C$
4960 T5=1
4970 T6=2
4980 P1=W1
4990 GOSUB 4350
5000 GO TO 4420
5010 REM
5020 REM EXPAND
5030 GOSUB 5540
5040 T7=68-T6
5050 B$=SEG(G$,1,T7)
5060 F$=REP(B$,T5,0)
5070 T6=68
5080 GOSUB 4350
5090 GOSUB 4420
5100 GOSUB 4560
5110 RETURN 
5120 REM COMPRESS
5130 GOSUB 5540
5140 I1=T5-1
5150 I1=I1+1
5160 B$=SEG(F$,I1,1)
5170 IF B$<>" " THEN 5190
5180 IF I1<T6 THEN 5150
5190 I2=I1-T5
5200 B$=SEG(C$,1,0)
5210 F$=REP(B$,T5,I2)
5220 T6=T6-I2
5230 GOSUB 4350
5240 GOSUB 4420
5250 RETURN 
5260 REM END
5270 REM         REPRINT LINE ----------------------------
5280 GOSUB 5540
5290 GOSUB 4350
5300 GOSUB 4420
5310 GO TO 4560
5320 REM---------------------------------------------------
5330 REM SUBROUTINE TO INPUT CHARACTERS
5340 GOSUB 5540
5350 MOVE P1,P2
5360 INPUT B$
5370 REM CHECK TO SEE IF F$ IS LONG ENOUGH TO ACCEPT B$
5380 T6=LEN(F$)
5390 I2=LEN(B$)
5400 T7=I2-(T6-T5)
5410 IF T7<=0 THEN 5450
5420 I$=SEG(G$,1,T7-1)
5430 F$=F$&I$
5440 F$=F$&C$
5450 F$=REP(B$,T5,I2)
5460 T5=T5+I2-1
5470 P1=P1+I2*W-W
5480 T6=LEN(F$)
5490 GOSUB 4350
5500 GOSUB 4420
5510 RETURN 
5520 REM
5530 REM--------------------------------------------------
5540 REM CHECK FOR NO LINE SELECTED
5550 E1=2
5560 IF LEN(F$)<1 THEN 5600
5570 E1=1
5580 RETURN 
5590 REM
5600 REM -------------------------------------------------
5610 REM HELP INFO ROUTINE
5620 PRINT "";
5630 WAIT 
5640 REM
5650 REM HELP ROUTINE
5660 GO TO E1 OF 5680,5810,5850,5900,5940,5940,5990,6030
5670 GO TO 5600
5680 REM PRINT FUNCTION KEY LABELS
5690 PRINT " LIST    FILLIN             CHANGE   RESTORE LINE"
5700 PRINT "LABELS   LABELS     OLD     SEQUENCE  TO LABEL"
5710 PRINT " ---      ---       ---       ---       ---"
5720 PRINT " HELP    ENTER      SAVE     PRINT    DISPLAY"
5730 PRINT USING "9X,6A,14X,6A,///":"LABEL","LABEL"
5740 PRINT 
5750 PRINT "         RUB OUT  RUB OUT              ENTER"
5760 PRINT "COMPRESS   <-        ->     REPRINT  CHARACTER"
5770 PRINT " ---      ---       ---       ---       ---"
5780 PRINT " EXPAND   BACK     SPACE    CLEAR     RECALL"
5790 PRINT USING "10X,6A,22X,6A":"SPACE","LINE"
5800 GO TO 6070
5810 REM ERROR #2
5820 GOSUB 4350
5830 PRINT "NO LINE AVAILABLE FOR EDITING. SEE KEY #10"
5840 GO TO 6070
5850 REM HELP MES #3
5860 GOSUB 4350
5870 PRINT "NO LABEL SELECTED FOR EDITING. SEE KEY #5"
5880 GO TO 6070
5890 REM
5900 REM HELP MESS #4
5910 GOSUB 4350
5920 PRINT "NO LABEL. SEE KEY #2"
5930 GO TO 6070
5940 REM HELP MESS #5
5950 GOSUB 4350
5960 PRINT "STILL HAVE LINE FOR EDITING. CLEAR(#9) OR REPLACE(#15)."
5970 E1=6
5980 GO TO 6070
5990 REM MESS #7
6000 GOSUB 4350
6010 PRINT "LABEL WOULD BE TOO LONG"
6020 GO TO 6070
6030 REM MESS #8
6040 GOSUB 4350
6050 PRINT "NO LINE TO REPLACE"
6060 GO TO 6070
6070 REM END OF CASE BRANCH
6080 E1=1
6090 WAIT 
6100 REM
6110 REM
6120 REM SAVE LABEL
6130 REM S1  LOCATION OF FIRST LABEL
6140 REM S2  LAST LABEL ACCESED
6150 REM S3  LOCATION OF LAST LABEL ON TAPE
6160 IF E1=6 THEN 6200
6170 E1=5
6180 IF T4>0 THEN 5600
6190 E1=1
6200 PRINT "LABEL TO BE STORED"
6210 PRINT D$,"ENTER RETURN FOR OK."
6220 INPUT B$
6230 IF LEN(B$)>0 THEN 6200
6240 PRINT "ENTER NUMBER OF LABEL TO SAVE"
6250 INPUT S2
6260 S2=S1+S2-1
6270 IF S2=>S1 AND S2<=S3 THEN 6300
6280 PRINT "SELECT A POSITIVE NUMBER FOR STORAGE. FIRST IS #1"
6290 GO TO 6240
6300 FIND S2
6310 WRITE @33:D$
6320 RETURN 
6330 REM
6340 REM OLD LABEL
6350 IF E1=6 THEN 6390
6360 E1=5
6370 IF T4>0 THEN 5600
6380 E1=1
6390 PRINT "ENTER NUMBER OF LABEL ";
6400 INPUT S2
6410 S2=S1+S2-1
6420 IF S2=>S1 AND S2<=S3 THEN 6450
6430 PRINT "ENTER A POSITIVE NUMBER. 1ST LABEL IS #1"
6440 GO TO 6390
6450 FIND S2
6460 IF TYP(0)=4 THEN 6490
6470 PRINT "LABEL NOT STORED THERE"
6480 GO TO 6340
6490 READ @33:A$
6500 PRINT A$
6510 RETURN 
6520 REM
6530 REM LIST LABELS
6540 PAGE 
6550 FOR I=S1 TO S3
6560 FIND I
6570 J=TYP(0)
6580 IF J<>4 THEN 6660
6590 READ @33:A$
6600 B$=SEG(A$,1,65)
6610 J=POS(B$,C$,1)
6620 IF J=0 THEN 6650
6630 B$=REP(" ",J,1)
6640 GO TO 6610
6650 PRINT USING "2D,X,65A":I-S1+1,B$
6660 NEXT I
6670 RETURN 
6680 REM DEBUG D$
6690 FOR I=1 TO LEN(D$)
6700 Z$=SEG(D$,I,1)
6710 Z=ASC(Z$)
6720 PRINT Z;
6730 NEXT I
6740 STOP 


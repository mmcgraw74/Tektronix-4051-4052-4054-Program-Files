100 INIT 
101 REM  ******************************************
102 REM  *                                        *
103 REM  *      PROGRAM WRITTEN AUGUST 1981       *
104 REM  *                 BY                     *
105 REM  *          RICHARD M ENGEMAN             *
106 REM  *                FOR                     *
107 REM  *    DENVER WILDLIFE RESEARCH CENTER     *
108 REM  *    U.S. FISH AND WILDLIFE SERVICE      *
109 REM  *                                        *
110 REM  ******************************************
111 PAGE 
120 PRINT "THIS PROGRAM CALCULATES THE TEST STATISTIC FOR THE"
130 PRINT "WILCOXON RANK SUM TEST ON DIFFERENCES BETWEEN"
140 PRINT "UNPAIRED REPLICATES"
150 PRINT 
160 PRINT " FOR REFERENCES SEE:"
170 PRINT "Wilcoxon,F. and Wilcox,R."
180 PRINT "   Some Rapid Approximate Statistical Procedures"
190 PRINT "Hollander and Wolfe"
200 PRINT "   Nonparametric Statistical Methods"
210 PRINT 
220 PRINT 
240 PRINT "IF YOU ARE READY TO INPUT DATA PRESS RETURN ";
250 INPUT U$
280 PAGE 
285 REM INPUTTING SAMPLE SIZES
290 PRINT "ENTER THE # OF ELEMENTS IN THE FIRST SAMPLE, X ";
300 INPUT N1
302 IF N1=>3 THEN 310
304 PRINT "THE X SAMPLE IS TOO SMALL (<3)"
306 PRINT "TYPE RUN AND START OVER WITH A NEW SAMPLE"
308 GO TO 1598
310 PRINT "ENTER THE # OF ELEMENTS IN THE SECOND SAMPLE, Y ";
320 INPUT N2
322 IF N2=>3 THEN 330
324 PRINT "THE Y SAMPLE IS TOO SMALL (<3)"
326 PRINT "TYPE RUN AND START OVER WITH A NEW SAMPLE"
328 GO TO 1598
330 N=N1+N2
340 DIM W(N),T(N),S(N),W1(N),S1(N),X(N1),Y(N2),L(N)
350 PAGE 
355 REM INPUTTING X DATA
360 PRINT "ENTER EACH ELEMENT OF X"
370 PRINT "PRESS RETURN AFTER EACH ENTRY"
380 PRINT 
390 Z=3
400 FOR I=1 TO N1
410 Z=Z+1
420 IF Z=34 THEN 460
430 PRINT "ENTER X";I;"     ";
440 INPUT W(I)
445 T(I)=1
450 GO TO 490
460 PAGE 
470 Z=1
480 GO TO 430
490 X(I)=W(I)
500 NEXT I
510 PAGE 
515 REM INPUTTING Y DATA
520 PRINT "ENTER EACH ELEMENT OF Y"
530 PRINT "PRESS RETURN AFTER EACH ENTRY"
540 PRINT 
550 Z=3
560 FOR I=1 TO N2
570 K=N1+I
580 Z=Z+1
590 IF Z=34 THEN 630
600 PRINT "ENTER Y";I;"     ";
610 INPUT W(K)
615 T(K)=2
620 GO TO 660
630 PAGE 
640 Z=1
650 GO TO 600
660 Y(I)=W(K)
670 NEXT I
680 PAGE 
685 REM PRINTING X DATA
686 B=1
690 D=32
695 PRINT @D:
700 PRINT @D:"HERE IS THE X DATA"
710 PRINT @D:
720 FOR I=1 TO N1
730 PRINT @D:"X";I;" = ";X(I)
740 NEXT I
745 PRINT 
750 PRINT "DO YOU WANT THIS PRINTED ON ANOTHER DEVICE? (Y OR N)"
760 INPUT U$
770 IF U$="Y" THEN 790
780 GO TO B OF 820,920
790 PRINT "ENTER DEVICE # ";
800 INPUT D
810 GO TO B OF 700,850
820 PAGE 
825 REM PRINTING Y DATA
830 B=2
840 D=32
850 PRINT @D:
860 PRINT @D:"HERE IS THE Y DATA"
870 PRINT @D:
880 FOR I=1 TO N2
890 PRINT @D:"Y";I;" = ";Y(I)
900 NEXT I
910 GO TO 745
920 PAGE 
925 REM DATA CORRECTION
930 PRINT "DO YOU WANT TO ALTER A DATA POINT (Y OR N)"
940 INPUT U$
950 IF U$="Y" THEN 961
960 GO TO 1205
961 PRINT "IS IT AN X OR Y DATA POINT? ";
962 INPUT U$
963 IF U$="X" THEN 967
964 IF U$="Y" THEN 969
965 GO TO 961
967 P=0
968 GO TO 970
969 P=N1
970 PRINT "ENTER THE FAULTY VALUE ";
980 INPUT G
990 PRINT "ENTER THE CORRECT VALUE ";
1000 INPUT H
1010 J=P+1
1015 N9=N+N2*(P-N1)/N1
1020 FOR K=J TO N9
1030 IF W(K)=G THEN 1070
1040 NEXT K
1045 PRINT ""
1050 PRINT "THE VALUE ";G;" IS NOT IN THE ";U$;" DATA"
1060 GO TO 1090
1070 W(K)=H
1080 PRINT "CORRECTED"
1090 PRINT 
1100 PRINT "DO YOU WANT TO ALTER ANOTHER DATA POINT? (Y OR N)"
1110 INPUT U$
1120 IF U$="Y" THEN 961
1130 FOR I=1 TO N1
1140 X(I)=W(I)
1150 NEXT I
1160 FOR I=1 TO N2
1170 K=N1+I
1180 Y(I)=W(K)
1190 NEXT I
1200 GO TO 680
1205 PRINT 
1206 PRINT "I HAVE TO CONCENTRATE NOW"
1210 GOSUB 1600
1215 PAGE 
1220 D=32
1230 PRINT "DO YOU WANT TO SEE THE SORTED DATA? (Y OR N)"
1240 INPUT U$
1250 IF U$="Y" THEN 1265
1260 GO TO 1420
1265 PAGE 
1270 PRINT @D:
1280 PRINT @D:"HERE IS THE SORTED DATA"
1290 PRINT @D:
1300 PRINT @D:"RANK               DATA          TREATMENT"
1310 PRINT @D:
1320 FOR I=1 TO N
1325 K=S(I)
1330 PRINT @D:S1(I),W(K),T(K)
1340 NEXT I
1350 PRINT 
1360 PRINT "DO YOU WANT THIS PRINTED ON ANOTHER DEVICE? (Y OR N)"
1370 INPUT U$
1380 IF U$="N" THEN 1420
1390 PRINT "ENTER DEVICE # WHERE IT IS TO BE PRINTED"
1400 INPUT D
1410 GO TO 1265
1420 J=1
1430 IF N2<N1 THEN 1450
1440 GO TO 1460
1450 J=2
1460 R=0
1470 FOR I=1 TO N
1473 K=S(I)
1475 K1=S1(I)
1480 IF T(K)=J THEN 1500
1490 GO TO 1510
1500 R=R+K1
1510 NEXT I
1540 D=32
1545 PAGE 
1550 PRINT @D:"THE TEST STATISTIC IS THE SUM OF THE RANKS FOR THE "
1560 PRINT @D:"SMALLER SAMPLE"
1570 PRINT @D:"              R = ";R
1575 PRINT @D:"        WITH N1 = ";N1
1577 PRINT @D:"         AND N2 = ";N2
1580 PRINT @D:
1590 PRINT @D:"FOR SIGNIFICANCE LOOK THIS VALUE UP IN ONE OF REFERENCES"
1591 PRINT "DO YOU WANT THIS PRINTED ON ANOTHER DEVICE? (Y OR N)"
1592 INPUT U$
1593 IF U$="N" THEN 1598
1594 PRINT "ENTER DEVICE # WHERE IT IS TO BE PRINTED"
1595 INPUT D
1596 GO TO 1545
1598 END 
1599 REM SORTING DATA
1600 FOR I=1 TO N
1610 S(I)=I
1620 NEXT I
1630 I2=N-1
1640 FOR I=1 TO I2
1650 I3=I+1
1660 FOR J=I3 TO N
1670 K1=S(J)
1680 K2=S(I)
1690 IF W(K1)>W(K2) THEN 1730
1700 I4=S(I)
1710 S(I)=S(J)
1720 S(J)=I4
1730 NEXT J
1740 NEXT I
1750 FOR I=1 TO N
1760 K=S(I)
1770 L(I)=1
1780 W1(I)=W(K)
1790 NEXT I
1800 I=1
1810 Z=1
1820 Z2=I
1825 IF I>N THEN 1960
1830 FOR J=I TO I2
1840 K=J+1
1845 IF K>N THEN 1890
1850 IF W1(J)<>W1(K) THEN 1890
1860 Z=Z+1
1870 Z2=Z2+J+1
1880 NEXT J
1890 Z1=Z-1+I
1900 FOR J=I TO Z1
1910 L(J)=Z
1920 S1(J)=Z2
1930 NEXT J
1940 I=I+Z
1950 GO TO 1810
1960 FOR I=1 TO N
1970 S1(I)=S1(I)/L(I)
1980 NEXT I
1990 REM LINES 1750 TO HERE AVERAGE RANKS WHERE NECESSARY
2000 RETURN 


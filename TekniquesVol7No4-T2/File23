100 REM STEREONET VECTOR MEAN AND POLE TO PIE CIRCLE
110 PAGE 
120 PRINT "THIS PROGRAM CALCULATES THE VECTOR MEAN OF A "
130 PRINT "DATA SET, AND CALCULATES THE BEST FIT"
140 PRINT "POLE TO PLANE FOR A GREAT CIRCLE."
150 PRINT 
160 PRINT "ENTER IN BINARY DATA FILE NUMBER:  ";
170 INPUT E
180 FIND E
190 READ @33:N
200 N1=N
210 GOSUB 1000
220 READ @33:X,Y,Z,P
230 CLOSE 
240 PAGE 
250 PRINT 
260 PRINT "          OPTIONS"
270 PRINT 
280 PRINT "           0= exits you from program"
290 PRINT "           1= calculates vector mean"
300 PRINT "           2= calculates best fit"
310 PRINT "              pie circle"
320 PRINT "           3= outputs to data file"
330 PRINT 
340 PRINT " ENTER OPTION NUMBER:  ";
350 INPUT E1
360 IF E1=0 THEN 390
370 GOSUB E1 OF 2000,3000,4000
380 GO TO 240
390 PAGE 
400 GOSUB 5000
410 IF E6<>0 THEN 110
420 END 
1000 REM SUBROUTINE TO DIMENSION VARIABLES
1010 DIM X(N),Y(N),Z(N),P(N)
1020 RETURN 
2000 REM SUBROUTINE TO CALCULATE VECTOR MEAN
2010 X1=0
2020 Y1=0
2030 Z1=0
2040 FOR I=1 TO N
2050 IF P(I)=6 OR P(I)=7 THEN 2090
2055 REM ** DIVIDES THE VALUE BY TEN SO THE SPHERE IS A UNIT SPHERE **
2056 REM ** SUMS THE VALUES OF X,Y,Z**
2060 X1=X(I)/10+X1
2070 Y1=Y(I)/10+Y1
2080 Z1=Z(I)/10+Z1
2090 NEXT I
2095 REM ** SQUARE ROOT OF THE SUM OF THE SQUARE OF THE SUMS OF X,Y,Z **
2100 T=SQR(X1^2+Y1^2+Z1^2)
2110 REM THE FOLLOWING SETS X2,Y2,Z2 EQUAL TO THE VECTOR MEAN
2120 X2=X1/T*10
2130 Y2=Y1/T*10
2140 Z2=Z1/T*10
2150 IF Z2=>0 THEN 2190
2160 X2=-X2
2170 Y2=-Y2
2180 Z2=-Z2
2190 PRINT "DO YOU WANT THE VECTOR MEAN ADDED TO THE DATA FILE:  "
2200 PRINT "(1=YES,2=NO):  ";
2210 INPUT E3
2220 IF E3=1 THEN 2250
2230 DELETE X1,Y1,Z1,X2,Y2,Z2,T
2240 RETURN 
2250 DELETE X1,Y1,Z1
2260 DIM X1(N+1),Y1(N+1),Z1(N+1),P1(N+1)
2270 FOR I=1 TO N
2280 X1(I)=X(I)
2290 Y1(I)=Y(I)
2300 Z1(I)=Z(I)
2310 P1(I)=P(I)
2320 NEXT I
2330 X1(N+1)=X2
2340 Y1(N+1)=Y2
2350 Z1(N+1)=Z2
2360 P1(N+1)=6
2370 DELETE X,Y,Z,P
2380 N=N+1
2390 GOSUB 1000
2400 X=X1
2410 Y=Y1
2420 Z=Z1
2430 P=P1
2440 DELETE X1,Y1,Z1,P1,X2,Y2,Z2,T
2450 RETURN 
3000 REM SUBROUTINE TO CALCULATE BEST FIT PIE POLE
3010 C1=0
3020 C2=0
3030 C3=0
3040 C4=0
3050 C5=0
3060 A=0
3070 B=0
3075 REM ** LEAST SQUARES BEST FIT OF THE POLE NORMAL TO THE DATA **
3080 FOR I=1 TO N
3090 IF P(I)=6 OR P(I)=7 THEN 3150
3100 C1=X(I)/10*Y(I)/10+C1
3110 C2=X(I)/10*Z(I)/10+C2
3120 C3=Y(I)/10*Z(I)/10+C3
3130 C4=(X(I)/10)^2+C4
3140 C5=(Y(I)/10)^2+C5
3150 NEXT I
3160 A=(C1*C3-C2*C5)/(C4*C5-C1^2)
3170 B=(C1*C2-C3*C4)/(C4*C5-C1^2)
3180 Z1=(1+A^2+B^2)^-0.5*10
3190 X1=A*Z1
3200 Y1=B*Z1
3210 PRINT " DO YOU WANT POLE ADDED TO DATA SET"
3220 PRINT "(1=YES,2=NO):  ";
3230 INPUT E3
3240 IF E3=1 THEN 3270
3250 DELETE X1,Y1,Z1,C1,C2,C3,C4,C5,A,B
3260 RETURN 
3270 DIM X2(N+1),Y2(N+1),Z2(N+1),P2(N+1)
3280 FOR I=1 TO N
3290 X2(I)=X(I)
3300 Y2(I)=Y(I)
3310 Z2(I)=Z(I)
3320 P2(I)=P(I)
3330 NEXT I
3340 X2(N+1)=X1
3350 Y2(N+1)=Y1
3360 Z2(N+1)=Z1
3370 P2(N+1)=7
3380 DELETE X,Y,Z,P
3390 N=N+1
3400 GOSUB 1000
3410 X=X2
3420 Y=Y2
3430 Z=Z2
3440 P=P2
3450 DELETE X1,Y1,Z1,X2,Y2,Z2,P2
3460 RETURN 
4000 REM SUBROUTINE TO OUTPUT DATA TO TAPE
4010 PRINT 
4020 PRINT 
4030 PRINT " You can output the data in memory to the same data file"
4040 PRINT " than you used for input.  HOWEVER if you have added data"
4050 PRINT " so that the number of data points currently in memory is"
4060 PRINT " GREATER than the original number of data points, not all"
4070 PRINT " of the data may be stored.  In this case it is advisable"
4080 PRINT " for you to create a new data file.!!!!!!"
4090 PRINT 
4100 PRINT " THE ORIGINAL NUMBER OF DATA POINTS IN THE FILE IS:  ";N1
4110 PRINT 
4120 PRINT " THE CURRENT NUMBER OF DATA POINTS IN THE FILE IS:  ";N
4130 PRINT 
4140 PRINT "ENTER OUTPUT FILE NUMBER (if not known enter 0):  ";
4150 INPUT E
4160 IF E=0 THEN 4270
4170 PRINT "Is this a NEW file or an OLD file (1=NEW,2=OLD):  ";
4180 INPUT E3
4190 IF E3=2 THEN 4220
4200 FIND E
4210 MARK 1,40*N+10*N
4220 KILL E
4230 FIND E
4240 WRITE @33:N,X,Y,Z,P
4250 CLOSE 
4260 RETURN 
4270 PAGE 
4280 TLIST 
4290 CALL "WAIT",5
4300 GO TO 4140
5000 PRINT " ENTER THE PROGRAM NUMBER THAT YOU WANT TO RUN"
5010 PRINT "      1= STEREONET PLOTTING PROGRAM"
5020 PRINT "      2= STEREONET ROTATION PROGRAM"
5030 PRINT "      3= STEREONET DATA EDITING PROGRAM"
5040 PRINT "      4= STEREONET VECTOR MEAN AND PIE-POLE PROGRAM"
5050 PRINT "      0= NOTHING BUT EXITS YOU FROM THIS PROGRAM"
5060 PRINT " ENTER PROGRAM NUMBER: ";
5070 INPUT E6
5080 IF E6=4 OR E6=0 THEN 5110
5090 FIND E6
5100 OLD 
5110 RETURN 


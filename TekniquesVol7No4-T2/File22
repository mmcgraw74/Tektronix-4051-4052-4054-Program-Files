100 INIT 
110 SET DEGREES 
120 REM STEREONET DATA EDITING ROUTINE
125 PAGE 
130 PRINT "STEREONET DATA EDITING ROUTINE"
140 PRINT " "
150 PRINT "ENTER DATA FILE (BINARY) NUMBER: ";
160 INPUT E
170 FIND E
180 READ @33:N
190 GOSUB 910
200 READ @33:X,Y,Z,P
210 CLOSE 
215 REM ** OPTIONS AVAILABLE FOR THIS PROGRAM **
220 PRINT "     OPTIONS"
230 PRINT " "
240 PRINT "      1= list data"
250 PRINT "      2= delete data"
260 PRINT "      3= add data"
270 PRINT "      4= change data"
275 PRINT "      5= output data file to TAPE"
280 PRINT " "
290 PRINT "         It is advisable to exercize option 1 first"
300 PRINT " "
310 PRINT "ENTER OPTION NUMBER (0 exits you from program):  ";
320 INPUT E1
330 IF E1=0 THEN 520
340 GOSUB 710
490 GOSUB E1 OF 1000,2000,3000,4000,5000
500 PRINT " "
510 GO TO 220
520 GOSUB 6000
530 IF E6<>0 THEN 125
540 END 
700 REM SUBROUTINE TO CALCULATE DIRECTION AND DIP
710 FOR I=1 TO N
720 D(I)=ASN(Z(I)/10)
730 IF D(I)=90 THEN 780
740 A1=ACS(Y(I)/(10*COS(D(I))))
750 IF X(I)=>0 THEN 790
760 A(I)=360-A1
770 GO TO 800
780 A1=0
790 A(I)=A1
800 IF P(I)=4 OR P(I)=5 THEN 850
810 D(I)=90-D(I)
820 A(I)=A(I)+180
830 IF A(I)<360 THEN 850
840 A(I)=A(I)-360
850 NEXT I
860 RETURN 
900 REM SUBROUTINE TO DIMENSION VARIABLES
910 DIM X(N),Y(N),Z(N),D(N),A(N),P(N)
920 RETURN 
1000 REM SUBROUTINE TO LIST DATA
1001 PAGE 
1002 PRINT 
1004 PRINT "Option no. 1  LIST DATA"
1006 PRINT 
1020 PRINT "ENTER OUTPUT DEVICE NUMBER (32=SCREEN, 40=LINE PRINTER): ";
1030 INPUT E2
1040 IF E2=32 THEN 1100
1050 PRINT " "
1060 PRINT "Make sure printer is ON, and the BAUD RATE is set"
1070 PRINT "to 300.  Press RETURN when this is accomplished";
1080 INPUT B$
1090 PRINT @37,26:1
1100 PAGE 
1105 REM ** HEADINGS FOR LISTING DATA **
1110 PRINT @E2: USING 1120:
1120 IMAGE 40T,"DATA"
1130 PRINT @E2:" "
1140 PRINT @E2:" "
1150 PRINT @E2: USING 1160:
1160 IMAGE 28T,"Direction",46T,"Amount",62T,"Data Type"  
1170 A$="LINEATION"
1180 B$="POLE TO PLANE"
1182 F$="VECTOR MEAN"
1183 G$="PIE POLE"
1185 PRINT 
1190 FOR I=1 TO N
1200 IF P(I)=4 OR P(I)=5 THEN 1230
1201 IF P(I)=6 THEN 1212
1202 IF P(I)=7 THEN 1214
1210 C$=B$
1211 GO TO 1240
1212 C$=F$
1213 GO TO 1240
1214 C$=G$
1220 GO TO 1240
1230 C$=A$
1235 REM ** PRINTS THE DATA **
1240 PRINT @E2: USING 1250:I,A(I),D(I),C$
1250 IMAGE "Data Point no.",16T,3D,30T,6D,45T,6D,60T,13A
1260 NEXT I
1270 PRINT @37,26:0
1290 RETURN 
2000 REM SUBROUTINE TO DELETE DATA FROM DATA FILE
2030 PAGE 
2031 PRINT 
2032 PRINT "Option no. 2  DELETE DATA from file"
2033 PRINT 
2034 PRINT 
2040 PRINT "ENTER NUMBER OF DATA POINTS TO BE DELETED:  ";
2050 INPUT E3
2055 PRINT 
2060 DELETE A,D
2065 DIM B$(2)
2070 DIM X1(N-E3),Y1(N-E3),Z1(N-E3),P1(N-E3)
2080 FOR I=1 TO E3
2081 I1=I
2082 IF I<5 THEN 2084
2083 I1=4
2084 GOSUB I1 OF 2450,2470,2490,2510
2090 PRINT USING 2100:I,B$
2100 IMAGE "ENTER",3D,2A,X,"DATA POINT TO BE DELETED"   
2110 INPUT E4
2120 IF E4=>1 AND E4<=N THEN 2150
2130 PRINT "INDEX IS OUT OF RANGE!!!!"
2140 GO TO 2090
2150 P(E4)=10
2160 NEXT I
2170 REM ** DELETES THE DATA **
2180 FOR I=1 TO N
2185 IF I+1>N THEN 2270
2190 IF P(I)<>10 THEN 2270
2200 FOR J=I+1 TO N
2210 X(J-1)=X(J)
2220 Y(J-1)=Y(J)
2230 Z(J-1)=Z(J)
2240 P(J-1)=P(J)
2250 NEXT J
2255 IF J=>N THEN 2270
2260 IF P(I)=10 THEN 2200
2270 NEXT I
2280 FOR I=1 TO N-E3
2290 X1(I)=X(I)
2300 Y1(I)=Y(I)
2310 Z1(I)=Z(I)
2320 P1(I)=P(I)
2330 NEXT I
2340 N=N-E3
2350 DELETE X,Y,Z,P
2360 GOSUB 910
2380 X=X1
2390 Y=Y1
2400 Z=Z1
2410 P=P1
2430 DELETE X1,Y1,Z1,P1,B$,I1
2440 RETURN 
2450 B$="st"
2460 RETURN 
2470 B$="nd"
2480 RETURN 
2490 B$="rd"
2500 RETURN 
2510 B$="th"
2520 RETURN 
3000 REM SUBROUTINE TO ADD DATA
3005 PAGE 
3010 PRINT 
3020 PRINT 
3030 PRINT "Option no. 3  ADD DATA"
3040 PRINT 
3050 PRINT "Enter number of data points to be added to file: ";
3060 INPUT E2
3070 DIM X1(N+E2),Y1(N+E2),Z1(N+E2),P1(N+E2)
3075 DELETE A,D
3076 REM ** COPIES THE DATA INTO A DUMMY ARRAY **
3080 FOR I=1 TO N
3090 X1(I)=X(I)
3100 Y1(I)=Y(I)
3110 Z1(I)=Z(I)
3120 P1(I)=P(I)
3130 NEXT I
3140 PRINT 
3142 DIM B$(2)
3145 REM ** ADDS THE DATA **
3150 FOR I=N+1 TO N+E2
3152 I1=I-N
3153 IF I-N<5 THEN 3155
3154 I1=4
3155 GOSUB I1 OF 2450,2470,2490,2510
3160 PRINT USING 3170:I-N,B$
3170 IMAGE "Enter",3D,2A,X,"Data Point to be ADDED"
3180 INPUT A,D,P1(I)
3190 IF P1(I)=4 OR P1(I)=5 THEN 3260
3200 REM CALCULATES X,Y,Z FOR POLE TO PLANE
3210 X1(I)=10*COS(90-D)*SIN(A+180)
3220 Y1(I)=10*COS(90-D)*COS(A+180)
3230 Z1(I)=10*SIN(90-D)
3240 GO TO 3290
3250 REM CALCULTES X,Y,Z FOR LINEATION
3260 X1(I)=10*COS(D)*SIN(A)
3270 Y1(I)=10*COS(D)*COS(A)
3280 Z1(I)=10*SIN(D)
3290 NEXT I
3295 REM ** DELETES THE ORIGINAL ARRAY **
3300 DELETE X,Y,Z,P,A,D,B$,I1
3310 N=N+E2
3315 REM ** REDIMENSIONS THE ORIGINAL ARRAY AND TRANSFERS **
3316 REM ** THE DATA FROM THE DUMMY ARRAY BACK INTO THE **
3317 REM ** ORIGINAL (REDIMENSIONED) ARRAY**
3320 GOSUB 910
3330 X=X1
3340 Y=Y1
3350 Z=Z1
3360 P=P1
3365 REM ** DELETES THE DUMMY ARRAY (memory saver) **
3370 DELETE X1,Y1,Z1,P1
3380 RETURN 
4000 REM SUBROUTINE TO CHANGE DATA
4010 PAGE 
4011 PRINT "Option no. 4  CHANGE DATA"
4012 PRINT 
4020 PRINT "ENTER INDEX OF ITEM TO BE CHANGED ";
4030 INPUT E2
4040 PRINT USING 4050:E2,A(E2),D(E2),P(E2)
4050 IMAGE "THE CURRENT VALUE OF",4D," IS:",31T,4D,"/",3D,45T,3D
4060 PRINT "CHANGE THIS TO ";
4070 INPUT A(E2),D(E2),P(E2)
4080 GO TO P(E2) OF 4150,4150,4150,4100,4100,4150,4150
4090 REM CALCULATES X,Y,Z FOR LINEATION
4100 X(E2)=10*COS(D(E2))*SIN(A(E2))
4110 Y(E2)=10*COS(D(E2))*COS(A(E2))
4120 Z(E2)=10*SIN(D(E2))
4130 GO TO 4180
4140 REM CALCULATES X,Y,Z FOR POLE TO PLANE
4150 X(E2)=10*COS(90-D(E2))*SIN(A(E2)+180)
4160 Y(E2)=10*COS(90-D(E2))*COS(A(E2)+180)
4170 Z(E2)=10*SIN(90-D(E2))
4180 PRINT "DO YOU WISH TO CHANGE ANOTHER POINT (1=YES, 2=NO): ";
4190 INPUT E2
4200 IF E2=1 THEN 4020
4210 RETURN 
5000 REM SUBROUTINE TO OUTPUT DATA TO TAPE
5005 PAGE 
5010 PRINT " "
5020 PRINT "Option no. 5  OUTPUT DATA FILE TO TAPE"
5030 PRINT 
5040 PRINT 
5050 PRINT "You can output the data in memory to the same data file"
5060 PRINT "that you used for input. HOWEVER if you have added data"
5070 PRINT "so that the number of data points currently in memory is"
5080 PRINT "GREATER than the original number of data points, not all"
5090 PRINT "of the data will be stored.  In this case it is advisable"
5100 PRINT "for you to create a new data file. "
5110 PRINT 
5120 PRINT "ENTER OUTPUT DATA FILE NUMBER (IF NOT KNOWN ENTER 0): ";
5130 INPUT E2
5140 IF E2=0 THEN 5240
5142 REM ** NEW AND OLD ARE FOR DATA FILES NOT PREVIOUSLY MARKED,**   
5143 REM ** AND PREVIOUSLY MARKED RESPECTIVLY **
5150 PRINT "Is this a NEW file or an OLD file (1=NEW, 2=OLD): ";
5160 INPUT E3
5170 IF E3=2 THEN 5200
5180 FIND E2
5190 MARK 1,40*N+10*N
5200 KILL E2
5207 FIND E2
5210 WRITE @33:N,X,Y,Z,P
5220 CLOSE 
5230 RETURN 
5240 PAGE 
5250 TLIST 
5260 CALL "WAIT",5
5270 GO TO 5120
6000 PRINT " ENTER THE PROGRAM NUMBER THAT YOU WANT TO RUN"
6010 PRINT "      1= STEREONET PLOTTING PROGRAM"
6020 PRINT "      2= STEREONET ROTATION PROGRAM"
6030 PRINT "      3= STEREONET DATA EDITING PROGRAM"
6040 PRINT "      4= STEREONET VECTOR MEAN AND PIE-POLE PROGRAM"
6050 PRINT "      0= NOTHING BUT EXITS YOU FROM THIS PROGRAM"
6060 PRINT " ENTER PROGRAM NUMBER: ";
6070 INPUT E6
6080 IF E6=3 OR E6=0 THEN 6110
6090 FIND E6
6100 OLD 
6110 RETURN 


4 GO TO 100
64 FIND 1
65 OLD 
66 END 
68 FIND 6
69 OLD 
70 END 
72 RUN 
100 REM *** SLIDE MAKER PROGRAM ***
110 INIT 
113 DIM C$(2),I$(1),L$(1),T$(1)
115 WINDOW 0,150,0,100
116 VIEWPORT 0,150,0,100
120 PRINT @1,25:0
130 SET KEY 
140 T$=""
150 L$=""
160 PRINT "
 SET LOWER LEFT AND UPPER RIGHT OF PLOTTER WINDOW

"
170 PRINT "
T= TITLES    B= BOX    L= LINE    D= DIAMOND"
190 PRINT "P= POSITION    I= DUPLICATE LAST BOX, LINE OR DIAMOND DRAWN"
200 PRINT "R= RIGHT JUSTIFY  V= DISPLAY VALUES  ""CR""= CENTER TEXT : ";
210 L$=T$
215 M=0
220 INPUT T$
225 IF T$<>"I" THEN 230
226 T$=I$
227 M=1
230 IF T$="B" THEN 810
240 IF T$="L" THEN 950
250 IF T$="D" THEN 1040
260 IF T$="V" THEN 1160
270 PRINT "
 ENTER TEXT OF LINE : ";
280 INPUT A$
290 T=0
300 IF T$<>"T" THEN 320
310 T=1
320 PRINT "ENTER X FACTOR ; ";
330 INPUT X$
340 IF X$="NO" THEN 170
350 IF X$="" THEN 370
360 X=VAL(X$)
370 PRINT "ENTER Y FACTOR : ";
380 INPUT Y$
390 IF Y$="NO" THEN 170
400 IF Y$="" THEN 420
410 Y=VAL(Y$)
420 PRINT "POSITION PEN TO STARTING POSITION AND HIT 'RETURN'"
430 INPUT C$
440 IF C$="NO" THEN 170
450 REM READ POSITION OF PEN INTO X1,Y1
460 GIN @1:X1,Y1
470 REM SEND 4662 WIDTH & HEIGHT OF CHARACTERS
480 PRINT @1,17:X,Y
490 MOVE @1:X1,Y1
500 X3=X1
510 IF T$="P" THEN 710
520 IF T$<>"R" THEN 610
530 REM RIGHT JUSTIFY STRING
540 INPUT @1,24:X2,Y2
550 MOVE @1:X2,Y2
560 FOR I=1 TO LEN(A$)
570 PRINT @1:"";
580 NEXT I
590 GO TO 710
600 REM MOVE OUT LEN(A$) AND READ COORDINATE POSITION
610 FOR I=1 TO LEN(A$)
620 PRINT @1:" ";
630 NEXT I
640 INPUT @1,24:X2,Y2
650 REM WILL IT FIT INSIDE THE WINDOW ?
660 IF X2<150 THEN 690
670 PRINT A$;" (WILL NOT FIT AT CHAR-WIDTH OF ";X
680 GO TO 320
690 X1=(150-(X2-X1))/2
700 MOVE @1:X1,Y1
710 PRINT @1:A$;
720 IF T=0 THEN 790
730 REM REPRINT TITLE 5 TIMES, SLIGHTLY UP AND TO RIGHT EACH TIME.
740 FOR I=0.1 TO 0.5 STEP 0.1
750 MOVE @1:X1+I,Y1+I
760 PRINT @1:A$;
770 NEXT I
780 REM MOVE BACK TO LAST MANUALLY SELECTED POSITION, DOWN 1 CHAR HEIGT
790 MOVE @1:X3,Y1-Y
800 GO TO 170
810 REM USE CALL BUTTON TO DEFINE CORNERS
820 PRINT "
 MOVE PEN TO LOWER LEFT CORNER OF BOX"
830 INPUT @1,27:X3,Y3
831 IF M=0 THEN 840
832 GOSUB 1260
834 GO TO 880
840 PRINT @1:"";
850 PRINT "
 MOVE PEN TO UPPER RIGHT CORNER OF BOX"
860 REM READ THE CALL BUTTON
870 INPUT @1,27:X4,Y4
880 PRINT @1:"";
890 MOVE @1:X3,Y3
900 DRAW @1:X4,Y3
910 DRAW @1:X4,Y4
920 DRAW @1:X3,Y4
930 DRAW @1:X3,Y3
931 GOSUB 1290
940 GO TO 170
950 PRINT "
POSITION PEN AT START OF LINE"
960 INPUT @1,27:X3,Y3
961 IF M=0 THEN 970
962 GOSUB 1260
964 GO TO 1000
970 PRINT @1:"";
980 PRINT "POSITION PEN AT END OF LINE"
990 INPUT @1,27:X4,Y4
1000 PRINT @1:"";
1010 MOVE @1:X3,Y3
1020 DRAW @1:X4,Y4
1021 GOSUB 1290
1030 GO TO 170
1040 PRINT "
POSITION PEN AT TOP OF DIAMOND"
1050 INPUT @1,27:X3,Y3
1051 IF M=0 THEN 1060
1052 GOSUB 1260
1054 GO TO 1090
1060 PRINT @1:"";
1070 PRINT "POSITION PEN AT RIGHT CORNER OF DIAMOND"
1080 INPUT @1,27:X4,Y4
1090 PRINT @1:"";
1100 MOVE @1:X3,Y3
1110 DRAW @1:X4,Y4
1120 DRAW @1:X3,Y4-(Y3-Y4)
1130 DRAW @1:X3+(X3-X4),Y4
1140 DRAW @1:X3,Y3
1141 GOSUB 1290
1150 GO TO 170
1160 REM DISPLAY PRESENT VALUES
1170 PRINT "        PRESENT VALUES......


"
1180 PRINT " X FACTOR              : ";X
1190 PRINT " Y FACTOR              : ";Y
1200 INPUT @1,24:X2,Y2
1210 PRINT " PRESENT PEN POSITION  : ";X2;", ";Y2
1220 PRINT " LAST TEXT WAS (BELOW) :"
1230 PRINT A$
1240 PRINT " PREVIOUS FUNCTION WAS : ";L$
1250 GO TO 170
1260 X4=X4-(X5-X3)
1270 Y4=Y4-(Y5-Y3)
1280 RETURN 
1290 X5=X3
1300 Y5=Y3
1310 I$=T$
1320 RETURN 


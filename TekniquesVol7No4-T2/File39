100 REM{TAPE DUMP ROUTINE}
110 REM
120 REM       THIS PROGRAM  CHECKS THE
130 REM       DATA FORMAT TYPE OF EACH
140 REM       ITEM  ON  THE  TAPE  AND
150 REM       READS THE DATA USING THE
160 REM       CORRECT  FORM  FOR  THAT
170 REM       FORMAT.
180 REM
190 REM       INPUT  CHARACTER STRINGS
200 REM       ARE SCANNED  FOR CONTROL
210 REM       CHARACTERS AND,  IF PRE-
220 REM       SENT,  THEY ARE REPLACED
230 REM       WITH  AN  UPARROW-LETTER
240 REM       DESIGNATION.
250 REM
260 REM       EVERY  FILE  ON THE TAPE
270 REM       IS READ  (UNTIL AN EMPTY
280 REM       FILE IS FOUND)   AND  IS
290 REM       SENT TO THE LINE PRINTER.
300 REM
310 REM
320 F=1
330 DIM A$(200)
340 PRINT "WHAT IS THE STARTING FILE NUMBER ?";
350 INPUT F
360 PRINT "WHAT IS THE LAST FILE NUMBER ?";
370 INPUT F1
380 F1=F1+1
390 FIND F
400 PRINT @41:"FILE: ";F;"

"
410 IF F=F1 OR F>F1 THEN 440
420 GO TO TYP(0) OF 470,500,550,590
430 PRINT @41:"FILE EMPTY/LAST FILE"
440 PRINT @41:""
450 PRINT "DONE."
460 END 
470 REM *** NEXT DATA ITEM = EOF ***
480 F=F+1
490 GO TO 390
500 REM *** NEXT DATA ITEM = ASCII FORMAT ***
510 INPUT @33:A$
520 GOSUB 640
530 PRINT @41:A$
540 GO TO 420
550 REM *** NEXT DATA ITEM = NUM/BINARY FORMAT ***
560 READ @33:A
570 PRINT @41:A
580 GO TO 420
590 REM *** NEXT DATA ITEM = CHAR/BINARY FORMAT ***
600 READ @33:A$
610 GOSUB 640
620 PRINT @41:A$
630 GO TO 420
640 REM *** CHECK A$ FOR PRESENCE OF CONTROL CHARACTERS ***
650 IF NOT(LEN(A$)) THEN 740
660 C$=" ^G^H^I^J^K^L^["
670 FOR I=1 TO LEN(A$)
680 X$=SEG(A$,I,1)
690 P=POS("	
",X$,1)
700 IF NOT(P) THEN 730
710 Z$=SEG(C$,P*2,2)
720 A$=REP(Z$,I,1)
730 NEXT I
740 RETURN 


1 REM     XAXIS,YAXIS: DRAW LOG TIC MARKS ON X OR Y AXIS
100 REM  -------DRIVER FOR XAXIS ROUTINE---------
110 LIST 100
120 LIST 540
130 REM  PLOT LOG TIC MARKS ON ABSCISSA AXIS
140 PRINT "ENTER PLOT DEVICE #= ";
150 INPUT I3
160 PAGE 
170 VIEWPORT 0,130+20*(I3=12),0,100
180 REM   "ENTER #S FOR WINDOW COMMAND, X1,X2,Y1,Y2"
190 REM   ASSUME THAT X IS A LOG AXIS FROM 1E2 TO 1E6
200 DATA 2,6,100,220
210 READ X1,X2,Y1,Y2
220 WINDOW X1,X2,Y1,Y2
230 AXIS @I3:1,10,X1,Y1
240 AXIS @I3:1,10,X2,Y2
250 DATA 1,0.5,0
260 READ W,W1,W2
270 GOSUB 330
280 CALL "WAIT",2
290 GO TO 540
300 REM
310 REM   ---------------END DRIVER--------------------
320 REM
330 REM  START LXAXIS SUBROUTINE
340 REM   DEFAULT FOR W AND W1:   W=1  W1=0.5 (W2=0 OR -1)
350 REM   DEFAULT FOR X1,X2,Y1,Y2:  SET IN EXTERNAL WINDOW STATEMENT.
360 REM W AND W1 CONTROL TIC MARK SIZE. W1 IS FOR THE DECADES.
370 REM W2=0: FOWARD LOG TIC.  W2=-1: REVERSE LOG TIC
380 REM IF X1,X2,Y1,Y2 ARE NOT SPECIFIED, USE THE MOST RECENT WINDOW
390 REM                STATEMENT TO DEFINE X1,X2,Y1,Y2.
400 DASH 0  !  SEE NOTE
410 WINDOW X1,X2,Y1,Y2
420 FOR Y0=Y1 TO Y2 STEP Y2-Y1
430 FOR I=X1 TO X2
440 FOR J=1-8*W2 TO 9+8*W2 STEP 1+2*W2
450 MOVE @I3:(1+2*W2)*LGT(J)+I,Y0
460 SCALE 1,1
470 RDRAW @I3:0,(W+W1*(J=1))*(1-2*(Y0<>Y1))
480 WINDOW X1,X2,Y1,Y2
490 NEXT J
500 NEXT I
510 NEXT Y0
520 RETURN   !  ---------------END LXAXIS ROUTINE---------------
530 REM
540 REM  -------DRIVER FOR YAXIS ROUTINE---------
550 REM  PLOT LOG TIC MARKS ON ORDINATE AXIS
560 PAGE 
570 VIEWPORT 0,130+20*(I3=12),0,100
580 REM   "ENTER #S FOR WINDOW COMMAND, X1,X2,Y1,Y2"
590 REM   ASSUME THAT Y IS A LOG AXIS FROM 1E2 TO 1E6
600 DATA 100,220,2,6
610 RESTORE 600
620 READ X1,X2,Y1,Y2
630 WINDOW X1,X2,Y1,Y2
640 AXIS @I3:10,1,X1,Y1
650 AXIS @I3:10,1,X2,Y2
660 DATA 1,0.5,0
670 READ W,W1,W2
680 GOSUB 730
690 GO TO 930
700 REM
710 REM   ---------------END DRIVER--------------------
720 REM
730 REM  START LYAXIS SUBROUTINE
740 REM   DEFAULT FOR W AND W1:   W=1  W1=0.5  (W2=0 OR -1)
750 REM   DEFAULT FOR X1,X2,Y1,Y2:  SET IN EXTERNAL WINDOW STATEMENT.
760 REM W AND W1 CONTROL TIC MARK SIZE. W1 IS FOR THE DECADES.
770 REM W2=0: FOWARD LOG TIC.  W2=-1: REVERSE LOG TIC
780 REM IF X1,X2,Y1,Y2 ARE NOT SPECIFIED, USE THE MOST RECENT WINDOW
790 REM                STATEMENT TO DEFINE X1,X2,Y1,Y2.
800 DASH 0  !  SEE NOTE
810 WINDOW X1,X2,Y1,Y2
820 FOR X0=X1 TO X2 STEP X2-X1
830 FOR I=Y1 TO Y2
840 FOR J=1-8*W2 TO 9+8*W2 STEP 1+2*W2
850 MOVE @I3:X0,(1+2*W2)*LGT(J)+I
860 SCALE 1,1
870 RDRAW @I3:(W+W1*(J=1))*(1-2*(X0<>X1)),0
880 WINDOW X1,X2,Y1,Y2
890 NEXT J
900 NEXT I
910 NEXT X0
920 RETURN   !  ---------------END LYAXIS ROUTINE---------------
930 REM
940 REM     NOTE: STATEMENTS 395 AND 815 (DASH 0) SHOULD BE LOCAL
950 REM           TO THIS ROUTINE ONLY.  THE DASH STATEMENT IN THE
960 REM           CALLING ROUTINE SHOULD BE RESTORED UPON RETURN.


99 REM 51/00-8030/0
100 REM Program to trigger and read data from HP3438A multimeter
105 REM
110 REM J. R. Matey  RCA Labs, Princeton, NJ  08540   4 Jan 1979
115 REM
120 REM Note that the remote enable line (REN) is only active while
125 REM     a program is executing!
130 REM     The first listen command to the meter turns it to remote.
135 REM     The meter may be triggered by issuing the GET command:
140 REM     an octal 10 with the ATTN line pulled down.
145 REM
150 REM A1-GPIB address of the HP3438A
155 REM I$-input string
160 REM W$-working string
165 REM R1-reading
170 REM F1-function
175 REM ----------------------------------------------------------------
180 REM
185 INIT
190 REM Change page full condition
195 PRINT @32,26:2
200 DIM I$(13),W$(9)
205 A1=23
210 REM ----------------------------------------------------------------
215 REM Place HP3438A in remote mode (MLA,UNLISTEN,UNTALK)
220 WBYTE @A1+32,63,95:
225 REM Trigger the HP3438A (UNL,UNT,MLA,GET,UNL,UNT)
230 WBYTE @63,95:
235 WBYTE @A1+32:
240 WBYTE @8:
245 WBYTE @63,95:
250 REM Read ASCII output string from the meter
255 INPUT @A1,32:I$
260 REM Extract reading from string and convert to numeric form
265 W$=SEG(I$,1,9)
270 D1=VAL(W$)
275 REM Extract function from string and convert to numeric form
280 W$=SEG(I$,11,1)
285 F1=VAL(W$)
290 REM
295 PRINT D1,F1
300 GO TO 235
305 STOP
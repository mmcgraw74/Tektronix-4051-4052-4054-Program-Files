1000 REM ************************* 
1010 IF INT(P9/10)<>0 THEN 510
1020 GO TO P9 OF 1050,1650,1310,1850,1540
1030 REM ************************* 
1040 REM INITIALIZE MATRIX 
1050 SET NOKEY 
1060 PRINT "	EIGENANALYSIS OF COMPLEX HERMITIAN MATRIX"
1070 PRINT "J_ENTER # OF ROWS (=COLUMNS) OF MATRIX = ";
1080 INPUT N
1090 N=INT(N)
1100 DELETE A1,A2,P$,Q$
1110 DIM A1(N,N),A2(N,N),P$(1),Q$(4)
1120 A1=0
1130 A2=0
1140 PRINT "J_ENTER THE ENTIRE MATRIX BEFORE USING ANY OTHER KEYS."
1150 PRINT "J_ENTER COMPLEX VALUES AS REAL NUMBER PAIRS X,Y"
1160 PRINT "BOTH ENTRIES ARE ALWAYS REQUIRED"
1170 PRINT "J_ENTER LOWER TRIANGULAR ELEMENTS OF MATRIX *A*"
1180 FOR P1=1 TO N
1190 PRINT "J_COLUMN ";P1
1200 FOR P2=P1 TO N
1210 PRINT "A(";P2;",";P1;") = ";
1220 INPUT A1(P2,P1),A2(P2,P1)
1230 NEXT P2
1240 NEXT P1
1250 SET KEY 
1260 GOSUB 1450
1270 PRINT "J_ENTRY COMPLETE"
1280 RETURN 
1290 REM ************************* 
1300 REM CORRECT MATRIX
1310 PRINT "J_CORRECT THE MATRIX: ENTER ROW AND COLUMN"
1320 PRINT "OF THE ELEMENT TO BE CORRECTED"
1330 PRINT "J_ROW,COLUMN = ";
1340 INPUT P1,P2
1350 IF P2<=P1 THEN 1390
1360 P3=P1
1370 P1=P2
1380 P2=P3
1390 PRINT "PRESENT VALUE   = (";A1(P1,P2);",";A2(P1,P2);")"
1400 PRINT "ENTER NEW VALUE = ";
1410 INPUT A1(P1,P2),A2(P1,P2)
1420 IF P2<>P1 THEN 1440
1430 GOSUB 1450
1440 GO TO 1330
1450 FOR P3=1 TO N
1460 IF A2(P3,P3)<>0 THEN 1490
1470 NEXT P3
1480 RETURN 
1490 PRINT "J_YOU HAVE NOT ENTERED AN HERMITIAN MATRIX."
1500 PRINT "THE DIAGONAL ELEMENTS MUST BE REAL NUMBERS."
1510 RETURN 
1520 REM ************************* 
1530 REM PRINT MATRIX
1540 PRINT "EIGENANALYSIS OF COMPLEX HERMITIAN MATRIX"
1550 PRINT "*A* MATRIX (LOWER TRIANGULAR PORTION)"
1560 FOR P1=1 TO N
1570 PRINT "J_COLUMN ";P1
1580 FOR P2=P1 TO N
1590 PRINT "(";A1(P2,P1);",";A2(P2,P1);")"
1600 NEXT P2
1610 NEXT P1
1620 RETURN 
1630 REM ************************* 
1640 REM DATA FROM TAPE
1650 P4=33
1660 Q$="FROM"
1670 PRINT "	EIGENANALYSIS OF COMPLEX HERMITIAN MATRIXJ_"
1680 DELETE A1,A2,P$
1690 GOSUB 1960
1700 READ @P4:N
1710 DIM A1(N,N),A2(N,N),P$(1)
1720 A1=0
1730 A2=0
1740 FOR P1=1 TO N
1750 FOR P2=1 TO P1
1760 READ @P4:A1(P1,P2),A2(P1,P2)
1770 NEXT P2
1780 NEXT P1
1790 PRINT "J_RE-INSERT PROGRAM TAPE."
1800 PRINT "J_";Q$;" TAPE COMPLETE"
1810 DELETE Q$
1820 RETURN 
1830 REM ************************* 
1840 REM DATA TO TAPE
1850 P4=33
1860 Q$="TO"
1870 GOSUB 1950
1880 WRITE @P4:N
1890 FOR P1=1 TO N
1900 FOR P2=1 TO P1
1910 WRITE @P4:A1(P1,P2),A2(P1,P2)
1920 NEXT P2
1930 NEXT P1
1940 GO TO 1790
1950 PAGE 
1960 PRINT Q$;" TAPE"
1970 PRINT "J_INSERT DATA TAPE AND ENTER THE TAPE FILE NUMBER = ";
1980 INPUT P3
1990 FIND P3
2000 RETURN 




1000 REM *
1010 REM *  "FORMATTED" LISTING PROGRAM #2
1020 REM *           DAN TAYLOR, GCS SOFT. ENGR., DEC 1975
1030 REM *
1040 PRINT """FORMATTED"" LISTING PROGRAM -- ";
1050 PRINT "FOR A PRINTER WITHOUT A ""FORM FEED""."
1060 INIT 
1070 REM *
1080 PRINT "J_INITIALIZATION DEPENDENT UPON YOUR PRINTER:J_"
1090 LIST 1100,1220
1100 DATA 41,12,66,60,1000
1110 READ S3,S4,S5,S6,S7
1120 REM *            S3 = GPIB PRIMARY ADDRESS OF THE PRINTER.
1130 REM *            S4 = SECONDARY ADDRESS FOR THE PRINTER.
1140 REM *            S5 = TOTAL NUMBER OF PRINTABLE LINES PER PAGE.
1150 REM *            S6 = NUMBER OF LINES TO PRINT PER PAGE <= S5.
1160 REM *            S7 = NUMBER OF CHARACTERS YOUR PRINTER BUFFER
1170 REM *                 CAN HOLD.
1180 REM *            S$ = STRING WHICH WILL CAUSE YOUR PRINTER
1190 REM *                 TO "CARRIAGE RETURN & LINE FEED" ONE LINE.
1200 DIM S$(2)
1210 S$=CHR(13)
1220 S$=" "&S$
1230 REM *
1240 PRINT "J_INSERT YOUR PROGRAM TAPE WITH THE FILES TO BE LISTED."
1250 PRINT "J_START WITH TAPE FILE NUMBER = ";
1260 INPUT S8
1270 PRINT "J_  END WITH TAPE FILE NUMBER = ";
1280 INPUT S9
1290 PRINT "J_HOW MANY SPACES DO YOU WANT EACH ""FOR--NEXT"" LOOP ";
1300 PRINT "INDENTED = ";
1310 INPUT S0
1320 S0=INT(ABS(S0))
1330 DATA 0,1,2,32,64
1340 READ W0,W1,W2,T2,T6
1350 T3=S5-S6
1360 DIM A$(S7+500),B$(W2),I$(130),L$(210),R$(W1),T$(S0 MAX W1)
1370 DIM U$(T3*LEN(S$) MAX W1),Z$(3)
1380 PRINT "J_DO YOU WANT SEQUENCES OF ""REM"" STATEMENTS SEPARATED"
1390 PRINT "FROM THE REST OF THE CODE BY A BLANK LINE          : ";
1400 INPUT R$
1410 T7=W0
1420 IF R$="Y" THEN 1450
1430 IF R$<>"N" THEN 1380
1440 T7=W1
1450 A$=""
1460 T$=""
1470 IF S0=W0 THEN 1510
1480 FOR J=W1 TO S0
1490 T$=T$&" "
1500 NEXT J
1510 I$=""
1520 U$=""
1530 IF T3=W0 THEN 1570
1540 FOR I=W1 TO T3
1550 U$=U$&S$
1560 NEXT I
1570 B$=CHR(8)
1580 B$=B$&"_"
1590 PAGE 
1600 FOR J9=S8 TO S9
1610 REM                 ***** PREPARE FOR NEXT TAPE FILE *****
1620 I$=""
1630 S1=W1
1640 S2=W0
1650 ON EOF (0) THEN 2110
1660 PRINT "J_FILE #";J9;""
1670 FIND J9
1680 IF TYP(0)<>2 THEN 2170
1690 IF LEN(A$)<S7 THEN 1720
1700 PRINT @S3,S4:A$;
1710 A$=""
1720 INPUT @33:L$
1730 IF L$="" THEN 1720
1740 IF POS(L$,"REM",W1)=W0 AND POS(L$,"""",W1)=W0 THEN 1820
1750 FOR I=LEN(L$) TO W1 STEP -W1
1760 Z$=SEG(L$,I,W1)
1770 IF Z$=>" " THEN 1810
1780 Z$=CHR(ASC(Z$)+T6)
1790 Z$=Z$&B$
1800 L$=REP(Z$,I,W1)
1810 NEXT I
1820 L$=REP(S$,LEN(L$)+W1,W0)
1830 I=POS(L$," ",W2)+W1
1840 IF T7 THEN 1940
1850 J=POS(L$,"REM",I)=I
1860 IF J<>S1 THEN 1920
1870 A$=REP(S$,W1+LEN(A$),W0)
1880 S2=W1+S2
1890 IF S2<S6 THEN 1920
1900 A$=REP(U$,W1+LEN(A$),W0)
1910 S2=W0
1920 S1=W1-J
1930 IF J THEN 1980
1940 J=POS(L$,"NEXT",I)=I
1950 IF NOT(J) THEN 1980
1960 I$=REP("",W1,S0)
1970 REM                      ***** INSERT BLANKS FOR INDENTING? *****
1980 IF NOT(LEN(I$)) THEN 2000
1990 L$=REP(I$,I,W0)
2000 A$=REP(L$,W1+LEN(A$),W0)
2010 S2=W1+S2
2020 IF S2<S6 THEN 2050
2030 A$=REP(U$,W1+LEN(A$),W0)
2040 S2=W0
2050 IF J THEN 1690
2060 IF POS(L$,"FOR",I)<>I+LEN(I$) THEN 1690
2070 I$=REP(T$,W1,W0)
2080 GO TO 1690
2090 REM                      ***** FILE #J9 IS DONE *****
2100 IF LEN(A$)=W0 THEN 2130
2110 PRINT @S3,S4:A$;
2120 REM                      ***** FORM FEED TWICE *****
2130 A$=""
2140 FOR I=W1 TO S5+S5-S2
2150 A$=REP(S$,W1+LEN(A$),W0)
2160 NEXT I
2170 NEXT J9
2180 PRINT @S3,S4:A$;
2190 PRINT "J_	DONE"
2200 END 



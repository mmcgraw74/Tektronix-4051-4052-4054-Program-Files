100 REM         *****EXAMPLE OF THE USE OF THE TABLED t ROUTINES*****
110 PRINT "THIS DRIVER PROGRAM SHOWS HOW TO USE THE 3 TABLED t";
120 PRINT " SUBROUTINES:J_"
130 LIST 100,270
140 PRINT "J_    DEGREES OF"
150 PRINT "     FREEDOM 	     90%	     95%	     99%"
160 FOR J=1 TO 40
170 S3=J
180 GOSUB 1060
190 PRINT USING "6X,3D,10X,4D.3D,S":J,T3
200 GOSUB 1560
210 PRINT USING "10X,4D.3D,S":T3
220 GOSUB 2060
230 PRINT USING "10X,4D.3D":T3
240 NEXT J
250 END 
260 REM           *****END OF SAMPLE DRIVER*****
270 REM
1000 REM   *******************************************
1010 REM    GIVEN S3 DEGREES OF FREEDOM THIS SUBROUTINE WILL RETURN
1020 REM  THE TABLED T VALUE FOR A ONE TAILED TEST OF SIGNIFICANCE
1030 REM  LEVEL .95.  THIS VALUE IS USED TO SET 90% CONFIDENCE
1040 REM  LIMITS FOR THE MEAN. THE ROUTINE RETURNS T3 AS THE
1050 REM  TABLED T.
1060 IF S3>4 THEN 1090
1070 T3=15.016+S3*(-12.1829+S3*(3.8945-0.4135*S3))
1080 RETURN 
1090 T3=S3/(-0.559925368278+0.60784409253*S3)+6.0E-4
1100 RETURN 
1110 REM  END OF .95 LEVEL ROUTINE.
1120 REM   *******************************************
1130 REM
1500 REM   *******************************************
1510 REM    GIVEN S3 DEGREES OF FREEDOM THIS SUBROUTINE WILL RETURN
1520 REM  THE TABLED T VALUE FOR A ONE TAILED TEST OF SIGNIFICANCE
1530 REM  LEVEL .975.  THIS VALUE IS USED TO SET 95% CONFIDENCE
1540 REM  LIMITS FOR THE MEAN. THE ROUTINE RETURNS T3 AS THE
1550 REM  TABLED T.
1560 IF S3>4 THEN 1590
1570 T3=34.958+S3*(-31.3655+S3*(10.208-1.0945*S3))
1580 RETURN 
1590 T3=S3/(-0.6115993191+0.5101102332*S3)+6.0E-4
1600 RETURN 
1610 REM   END OF .975 TABLED T ROUTINE.
1620 REM   *******************************************
1630 REM
2000 REM   *******************************************
2010 REM    GIVEN S3 DEGREES OF FREEDOM THIS SUBROUTINE WILL RETURN
2020 REM  THE TABLED T VALUE FOR A ONE TAILED TEST OF SIGNIFICANCE
2030 REM  LEVEL .995. THIS VALUE IS USED TO SET 99% CONFIDENCE
2040 REM  LIMITS FOR THE MEAN. THE ROUTINE RETURNS T3 AS THE
2050 REM  TABLED T.
2060 IF S3>1 THEN 2090
2070 T3=63.657
2080 RETURN 
2090 IF S3>5 THEN 2120
2100 T3=35.362+S3*(-20.6568+S3*(4.6965-0.36367*S3))
2110 RETURN 
2120 T3=S3/(-0.715572179161+0.387490270184*S3)+6.0E-4
2130 RETURN 
2140 REM  END OF .995 LEVEL ROUTINE.
2150 REM   *******************************************



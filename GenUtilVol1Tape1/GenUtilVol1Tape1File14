100 REM        ******START OF BUBBLE SORT DRIVER ROUTINE******
110 DELETE A
120 PRINT "	** BUBBLE SORT **J_J_"
130 PRINT "THE SORT ALGORITHM ITSELF IS IN A SUBROUTINE STARTING AT";
140 PRINT " LINE #1000.	LINES #100-300 ARE AN EXAMPLE OF HOW TO ";
150 PRINT "USE THE SUBROUTINE:J_J_"
160 PRINT "HOW LARGE A RANDOM ARRAY WOULD YOU LIKE FOR A TEST SORT = ";
170 INPUT A0
180 DIM A(A0)
190 FOR I=1 TO A0
200 A(I)=INT(10*A0*RND(1)+1)
210 NEXT I
220 PRINT "J_	NUMBERS TO BE SORTED",A
230 GOSUB 1000
240 IF A0<=32 THEN 260
250 PAGE 
260 PRINT "J_	SORTED NUMBERS",A
270 END 
280 REM         ******END OF BUBBLE SORT DRIVER ROUTINE******
290 REM
300 REM *-----------------------------------------------------------*
310 REM
1000 REM              ******START OF BUBBLE SORT******
1010 REM VARIABLES:
1020 REM                  **INPUT**
1030 REM     A  - THE LINEAR ARRAY TO BE SORTED INTO
1040 REM          INCREASING ORDER
1050 REM     A0 - THE NUMBER OF ELEMENTS OF A
1060 REM                  **OUTPUT**
1070 REM     A  - THE SORTED ARRAY
1080 A2=INT(A0)+1
1090 IF A2<3 THEN 1220
1100 A3=0
1110 FOR A1=1 TO A2-2
1120 REM CHANGE < TO > FOR SORTING INTO DECREASING ORDER
1130 IF A(A1)<=A(A1+1) THEN 1190
1140 A3=A1+1
1150 REM SWITCH ELEMENTS
1160 A4=A(A1)
1170 A(A1)=A(A3)
1180 A(A3)=A4
1190 NEXT A1
1200 A2=A3
1210 GO TO 1090
1220 RETURN 
1230 REM               ******END OF BUBBLE SORT******



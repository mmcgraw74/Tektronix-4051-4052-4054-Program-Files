1000 REM  *----------------------------*ALPHABETIC STRING SORT*------*
1010 REM  *-------------------------------------*INPUT SECTION*------*
1020 PRINT "	** ALPHABETIC STRING SORT **J_J_"
1030 INIT 
1040 PRINT "ENTER THE LENGTH OF THE LONGEST ITEM (UP TO 72 CHARS)  = ";
1050 INPUT J1
1060 J1=INT(J1)
1070 IF J1>72 OR J1<1 THEN 1040
1080 PRINT "J_IF YOU ENTER AN ITEM WITH MORE THAN ";J1;" CHARACTERS ";
1090 PRINT "THEN THE	BELL WILL RING AND THE EXTRA CHARACTERS WILL ";
1100 PRINT "BE IGNORED."
1110 J7=J1+1
1120 DIM B$(J7),C$(1),D$(J1),E$(J1),F$(J1)
1130 J2=MEMORY-200
1140 J6=J2-J7
1150 PRINT "J_THERE IS ROOM FOR UP TO ";INT(J2/J7);" ITEMS."
1160 DIM A$(J2)
1170 A$=""
1180 E$=""
1190 FOR J=1 TO J1
1200 E$=E$&" "
1210 NEXT J
1220 C$=CHR(31)
1230 PRINT "J_ENTER THE ITEMS TO BE ALPHABETIZED, ONE PER LINE: "
1240 PRINT "(PRESS ""RETURN"" WITH NO ENTRY AFTER THE LAST ITEM IS ";
1250 PRINT "ENTERED.)J_"
1260 J3=0
1270 INPUT B$
1280 IF B$="" THEN 1410
1290 IF LEN(B$)<J7 THEN 1320
1300 PRINT "";
1310 B$=SEG(B$,1,J1)
1320 J3=J3+1
1330 F$=SEG(E$,1,J1-LEN(B$))
1340 F$=F$&C$
1350 B$=B$&F$
1360 A$=REP(B$,1+LEN(A$),0)
1370 IF LEN(A$)<J6 THEN 1270
1380 PRINT 
1390 PRINT J3;" ITEMS ENTERED--THE MAXIMUM POSSIBLE WITH YOUR MACHINE."
1400 REM  *--------------------------*SORTING (BUBBLE) SECTION*------*
1410 PRINT "J_SORTING THE ITEMS:"
1420 J0=J3+1
1430 IF J0<3 THEN 1600
1440 J4=0
1450 B$=SEG(A$,1,J1)
1460 J5=1
1470 FOR J=2 TO J0-1
1480 J5=J5+J7
1490 D$=SEG(A$,J5,J1)
1500 IF B$<=D$ THEN 1550
1510 J4=J
1520 A$=REP(B$,J5,J1)
1530 A$=REP(D$,J5-J7,J1)
1540 GO TO 1560
1550 B$=D$
1560 NEXT J
1570 J0=J4
1580 GO TO 1430
1590 REM  *----------------------------------*PRINTING SECTION*------*
1600 PRINT "ALPHABETIZED LIST -- ";J3;" ITEMSJ_"
1610 PRINT A$
1620 END 



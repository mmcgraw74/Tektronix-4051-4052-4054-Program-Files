100 REM FILE 2:DATA ENTRY-KEYBOARD
110 REM Part #062-3409-00
120 REM 4O5O SERIES GPIB SUPPORT SOFTWARE FOR 4662
130 INIT
140 PAGE
150 PRINT "~I~DATA ENTRY-KEYBOARD~_~"
160 DIM R$(90),A$(30),F$(1),D1(4),C$(30),B$(30)
170 DATA 0,0,0,0
180 RESTORE 170
190 READ D1
200 GO TO 310
210 REM--KEYBOARD ENTRY ROUTINE
220 INPUT A$
230 IF LEN(A$)<1 THEN 290
240 IF A$="END" THEN 880
250 FOR I9=1 TO LEN(A$)
260 F$=SEG(A$,I9,1)
270 IF 47<ASC(F$) AND ASC(F$)<58 THEN 300
280 NEXT I9
290 I9=0
300 RETURN
310 REM
320 PRINT "~J~Insert STORAGE TAPE and enter target file number ";
330 GOSUB 210
340 F=VAL(A$)
350 FIND F
360 C$="`DATA'X-AXISY-AXIS"
370 R$=""
380 FOR I=0 TO 2
390 B$=SEG(C$,I*6+1,6)
400 PRINT "Please enter ";B$;" title ";
410 GOSUB 210
420 F$=CHR(127)
430 A$=A$&F$
440 R$=R$&A$
450 NEXT I
460 C$="X minY minX maxY max"
470 FOR I=0 TO 3
480 B$=SEG(C$,I*5+1,5)
490 PRINT "Please enter ";B$;"imum value ";
500 GOSUB 210
510 IF I9=0 THEN 530
520 D1(I+1)=VAL(A$)
530 NEXT I
540 WRITE R$,1
550 WRITE D1
560 PAGE
570 N=1
580 ON EOF (0) THEN 850
590 PRINT "Type ""END"" to terminate input "
600 PRINT
610 FOR I=1 TO 28
620 PRINT USING """Index No. ""FD""    Enter X value ""S":N
630 INPUT @32,24:X,Y
640 GOSUB 210
650 IF I9<>0 THEN 680
660 PRINT @32,21:X,Y
670 GO TO 640
680 X9=VAL(A$)
690 PRINT @32,21:X,Y
700 B$="9X""; Enter Y value ""S"
710 C$=STR(LEN(A$)+1)
720 B$=REP(C$,1,1)
730 PRINT USING B$:
740 INPUT @32,24:X,Y
750 GOSUB 210
760 IF I9<>0 THEN 790
770 PRINT @32,21:X,Y
780 GO TO 750
790 Y9=VAL(A$)
800 WRITE X9,Y9
810 N=N+1
820 NEXT I
830 PAGE
840 GO TO 590
850 REM END-OF-FILE ERROR MESSAGE
860 PRINT USING "P""~G~~G~Target file too small! Remark & rerun""":
870 END
880 WRITE "END"
890 CLOSE
900 PRINT "FILE ";F;" CLOSED"
910 PRINT "Do you wish to create another file ~G~";
920 INPUT A$
930 IF A$="YES" OR A$="Y" THEN 100
940 PRINT "DATA ENTRY ended--Load SUPPORT TAPE and press ""AUTOLOAD"""
950 END

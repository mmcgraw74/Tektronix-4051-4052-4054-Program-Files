190 END 
200 REM  COMPLEX FUNCTIONS EXP, LN, 1/Z, Z^N, Z^1/N  ********** 
205 REM  REFN:  A. ABRAMOWITZ AND I.A. STEGUN (EDITORS),
210 REM         "HANDBOOK OF MATHEMATICAL FUNCTIONS", AMS 55, 
215 REM         DEPT. OF COMMERCE, WASHINGTON, D.C., 1964.
220 REM  Z = (X,Y) IN RECTANGULAR COORDINATES 
225 REM  Z2 = FUNC(Z1), ZJ = (XJ,YJ), J=1,2 
230 REM 
250 REM  Z2 = EXP(Z1)  ********** 
255 Y2=EXP(X1)
260 X2=Y2*COS(Y1)
265 Y2=Y2*SIN(Y1)
267 RETURN 
270 REM   END EXP **********
275 REM 
300 REM  Z2 = LN(Z1)  **********
305 GOSUB 350
310 IF X2<>0 THEN 325
315 PRINT "ERROR IN LN(Z1), Z1 = 0"
320 END 
325 X2=LOG(X2)
330 RETURN 
335 REM   END LN   ********** 
340 REM 
350 REM   RECTANGULAR TO POLAR  ********** 
355 X2=SQR(X1^2+Y1^2)
360 Y2=0
365 IF X2=0 THEN 385
370 Y2=ACS(X1/X2)
375 IF Y1=>0 THEN 385
380 Y2=-Y2
385 RETURN 
390 REM   END RECTANGULAR TO POLAR  **********
395 REM 
400 REM  Z2 = 1/Z1  **********
405 Y2=X1^2+Y1^2
410 IF Y2<>0 THEN 425
415 PRINT "ERROR IN 1/Z1, Z1 = 0"
420 END 
425 X2=X1/Y2
430 Y2=-Y1/Y2
432 RETURN 
435 REM   END 1/Z1  **********
440 REM 
450 REM  Z2 = Z1^N  **********
455 GOSUB 350
460 P9=X2^N
465 Y2=N*Y2
470 X2=P9*COS(Y2)
475 Y2=P9*SIN(Y2)
477 RETURN 
480 REM   END Z1^N  **********
485 REM 
500 REM  Z2 = Z1^(1/N)  **********
505 GOSUB 350
510 P9=X2^(1/N)
515 Y2=Y2/N
520 X2=P9*COS(Y2)
525 Y2=P9*SIN(Y2)
527 RETURN 
530 REM   END Z1^(1/N)  **********
535 REM





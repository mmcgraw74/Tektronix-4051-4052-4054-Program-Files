1 GO TO 5
3 DELETE 1,15
5 DELETE 1
7 PRINT "ENTER YOUR FUNCTION AS A SUBROUTINE STARTING AT LINE #1100."
9 PRINT "A SAMPLE FUNCTION IS PROVIDED:J_J_"
11 LIST 1100,1180
13 PRINT "J_J_"
15 END 
100 REM
110 REM  ***********************************************************
120 REM  SAMPLE USER WRITTEN DRIVER FOR GAUSS-N 
130 I6=0
140 PAGE 
150 PRINT 
160 PRINT "ENTER A,B = ";
170 INPUT A,B
180 PRINT 
190 PRINT "ENTER INITIAL #SUBINTERVALS, MAX. #BISECTIONS = ";
200 INPUT I7,I8
210 PRINT 
220 PRINT "ENTER #DIGITS ACCURACY DESIRED = ";
230 INPUT E
240 E=10^-E
250 PRINT 
260 GOSUB 1500
270 PRINT "INTEGRAL = ";I,"COMLETION FLAG = ";I9
280 PRINT 
290 PRINT "GAUSS'S 16-POINT FORMULA APPLIED ";I7;" TIMES."
300 PRINT 
310 PRINT 
320 GO TO 150
330 REM  END OF DRIVER FOR GAUSS-N
340 REM  ***********************************************************
350 REM 
1100 REM
1110 REM  ********************************************************** 
1120 REM  SAMPLE USER WRITTEN FUNCTION TO BE INTEGRATED. 
1130 Y=3*X^2
1140 RETURN 
1150 REM  END OF SAMPLE USER FUNCTION
1160 REM  ********************************************************** 
1170 REM 
1180 REM
1190 REM  ********************************************************** 
1200 REM  GAUSS-N -- NUMERICAL INTEGRATION BY N-POINT GAUSSIAN QUAD.
1210 REM             ROUTINE IS PRESENTLY SET UP FOR 16 POINT QUAD.
1220 REM  PARAMETERS 
1230 REM    A - LOWER BOUND OF INTERVAL (INPUT)
1240 REM    B - UPPER BOUND OF INTERVAL (INPUT)
1250 REM    E - UPPER BOUND OF ABSOLUTE ERROR DESIRED (INPUT)
1260 REM    I - APPROXIMATION OF THE INTEGRAL (OUTPUT) 
1270 REM    I6- MUST BE SET TO 0 THE FIRST TIME THE ROUTINE IS CALLED
1280 REM        SO THAT NECESSARY CONSTANTS WILL BE INITIALIZED.  DON'T
1290 REM        CHANGE IT IF GAUSS-N WILL BE CALLED AGAIN.  (INPUT)
1300 REM    I7- NUMBER OF TIMES FORMULA IS APPLIED IN (A,B) INITIALLY.
1310 REM        UPON RETURN FROM GAUSS-N IT IS THE TOTAL NUMBER OF
1320 REM        TIMES FORMULA WAS APPLIED IN (A,B).
1330 REM    I8- MAX. NUMBER OF TIMES I7 CAN BE DOUBLED TO ACHIEVE
1340 REM        DESIRED ACCURACY IN I. (INPUT) 
1350 REM    I9- COMPLETION FLAG (OUTPUT)
1360 REM  GOSUB 1100 - LOCATION OF THE USER SUPPLIED ROUTINE WHICH
1370 REM        EVALUATES THE FUNCTION BEING INTEGRATED.  Y = FUNC(X).
1380 REM 
1390 REM  I9=0:  NO ERROR - DESIRED ACCURACY REACHED.
1400 REM  I9=1:  ERROR - ACCURACY NOT REACHED DUE TO ROUNDING ERRORS.
1410 REM  I9=2:  ERROR - I8 IS TOO SMALL TO REACH DESIRED ACCURACY.
1420 REM  P0 - THE ORDER OF GAUSSIAN INTEGRATION PERFORMED - PRESENTLY
1430 REM       16. P0 MUST BE AN EVEN INTEGER.  TO CHANGE FROM 16 POINT
1440 REM       TO SOMETHING ELSE YOU MUST: (1) CHANGE THE VALUE OF P0 
1450 REM       BELOW;  (2) CHANGE THE "ZEROES" AND "WEIGHTS" BELOW
1460 REM 
1470 REM  ****************************************** 
1500 P0=16
1510 REM  ****************************************** 
1520 IF I6>0 THEN 1820
1530 DELETE P
1540 DIM P(P0+P0)
1550 REM 
1560 REM  **********SET ZEROES OF THE P0'TH LEGENDRE POLYNOMIAL****
1570 I6=1
1580 P(1)=-0.989400935
1590 P(2)=-0.944575023
1600 P(3)=-0.8656312024
1610 P(4)=-0.7554044084
1620 P(5)=-0.6178762444
1630 P(6)=-0.4580167776
1640 P(7)=-0.2816035508
1650 P(8)=-0.09501250984
1660 REM  *********SET P0'TH GAUSSIAN QUADRATURE WEIGHTS*******
1670 P(17)=0.02715245942
1680 P(18)=0.06225352394
1690 P(19)=0.09515851168
1700 P(20)=0.1246289713
1710 P(21)=0.1495959888
1720 P(22)=0.1691565194
1730 P(23)=0.1826034154
1740 P(24)=0.1894506105
1750 P2=P0+1
1760 P3=P0+P2
1770 FOR P1=1 TO P0/2
1780 P(P2-P1)=-P(P1)
1790 P(P3-P1)=P(P0+P1)
1800 NEXT P1
1810 REM  *******BEGIN INTEGRATION********** 
1820 I=0
1830 P8=0
1840 P4=B-A
1850 IF P4=0 THEN 2170
1860 FOR P1=1 TO I8
1870 P9=P8
1880 I9=I
1890 I=0
1900 FOR P2=1 TO I7
1910 REM 
1920 P7=0
1930 P5=(P2+P2-1)*P4/I7+A+A
1940 P6=P4/I7
1950 REM 
1960 FOR P3=1 TO P0
1970 X=0.5*(P5+P(P3)*P6)
1980 GOSUB 1100
1990 P7=P7+P((P3+P0))*Y
2000 NEXT P3
2010 REM 
2020 I=I+P7
2030 NEXT P2
2040 REM
2050 I=0.5*I/I7*P4
2060 IF P1=1 THEN 2110
2070 P8=ABS(I-I9)
2080 IF P8<E THEN 2170
2090 IF P1<5 THEN 2110
2100 IF P9<P8 THEN 2190
2110 I7=I7+I7
2120 NEXT P1
2130 REM 
2140 REM 
2150 I9=2
2160 RETURN 
2170 I9=0
2180 RETURN 
2190 I9=1
2200 RETURN 
2210 REM  ****************END OF GAUSS-N***********************
2220 REM  *****************************************************
2230 REM




1 GO TO 5
3 DELETE 1,15
5 DELETE 1
7 PRINT "ENTER YOUR FUNCTION AS A SUBROUTINE STARTING AT LINE #1100."
9 PRINT "A SAMPLE FUNCTION IS PROVIDED:J_J_"
11 LIST 1100,1180
13 PRINT "J_J_"
15 END 
100 REM 
110 REM  ***********************************************************
120 REM   SAMPLE USER WRITTEN DRIVER FOR AUTO-TRAP. INTEGRATION 
130 PAGE 
140 I9=0
150 PRINT 
160 PRINT "ENTER A,B = ";
170 INPUT A,B
180 PRINT 
190 PRINT "ENTER MAX. #BISECTIONS, #DIGITS ACCURACY  = ";
200 INPUT I8,E
210 E=10^-E
220 GOSUB 1500
230 PRINT 
240 PRINT "INTEGRAL = ";I,"COMPLETION FLAG = ";I9
250 PRINT 
260 PRINT 
270 GO TO 150
280 REM  END OF USER WRITTEN DRIVER 
290 REM  ***********************************************************
300 REM 
1100 REM 
1110 REM  ***********************************************************
1120 REM  SAMPLE USER WRITTEN FUNCTION TO BE INTEGRATED. 
1130 Y=3*X^2
1140 RETURN 
1150 REM  END OF USER WRITTEN FUNCTION 
1160 REM  ***********************************************************
1170 REM 
1180 REM 
1190 REM  ************************************************************
1200 REM  AUTOMATIC TRAPEZOIDAL INTEGRATON WITH ROMBERG EXTRAPOLATION
1210 REM
1220 REM  REF:  IBM 360 SCIENTIFIC SUBROUTINE PACKAGE, P.297 
1230 REM  REF:  BAUER, ALGORITHM 60, CACM, VOL.4, 6 (1961), P255.
1240 REM  PARAMETERS:
1250 REM    A - LOWER BOUND OF INTERVAL (INPUT)
1260 REM    B - UPPER BOUND OF INTERVAL (INPUT)
1270 REM    E - UPPER BOUND OF ABSOLUTE ERROR DESIRED (INPUT)
1280 REM    I - APPROXIMATION OF THE INTEGRAL (OUTPUT) 
1290 REM    I8- MAX. NUMBER OF BISECTIONS OF THE INTERVAL (A,B)
1300 REM        TO BE ALLOWED. 4 < I8 < 31  (INPUT)
1310 REM    I9- RESULTING ERROR FLAG (OUTPUT)
1320 REM    GOSUB 1100 - LOCATION OF USER SUPPLIED ROUTINE WHICH 
1330 REM      EVALUATES THE FUNCTION BEING INTEGRATED.  Y=FUNC(X).
1340 REM      X MUST NOT BE CHANGED BY THE SUBROUTINE
1350 REM  I9=0:  NO ERROR - DESIRED ACCURACY REACHED 
1360 REM  I9=1:  ERROR - ACCURACY NOT REACHED DUE TO ROUNDING ERRORS 
1370 REM  I9=2:  ERROR - I8 IS NOT IN THE RANGE (5-30) OR  A=B
1380 REM                 OR I8 IS TOO SMALL FOR DESIRED ACCURACY.
1390 REM
1500 DELETE P
1510 DIM P(I8+11)
1520 REM 
1530 REM  PREPARE FOR INTEGRATION LOOP 
1540 X=A
1550 GOSUB 1100
1560 P(11)=Y
1570 IF I8<5 THEN 2010
1580 IF I8>30 THEN 2010
1590 X=B
1600 GOSUB 1100
1610 P(11)=0.5*(Y+P(11))
1620 P1=B-A
1630 IF P1=0 THEN 2010
1640 P3=E/ABS(P1)
1650 P6=0
1660 P4=1
1670 P5=1
1680 REM 
1690 REM  BEGIN INTEGRATION LOOP 
1700 FOR P8=12 TO I8+11
1710 I=P(11)
1720 P(8)=P6
1730 P2=P1
1740 P1=0.5*P1
1750 P4=0.5*P4
1760 X=A+P1
1770 P0=0
1780 FOR P9=1 TO P5
1790 GOSUB 1100
1800 P0=P0+Y
1810 X=X+P2
1820 NEXT P9
1830 P(P8)=0.5*P(P8-1)+P4*P0
1840 REM
1850 REM  ROMBERG EXTRAPOLATION 
1860 P0=1
1870 FOR P9=11 TO P8-1
1880 P7=P8-P9+10
1890 P0=4*P0
1900 P(P7)=P(P7+1)+(P(P7+1)-P(P7))/(P0-1)
1910 NEXT P9
1920 REM
1930 REM  CHECK FOR CONVERGENCE 
1940 P6=ABS(I-P(11))
1950 IF P8>15 THEN 1970
1960 GO TO 1990
1970 IF P6<=P3 THEN 2050
1980 IF P6=>P(8) THEN 2070
1990 P5=2*P5
2000 NEXT P8
2010 I9=2
2020 I=(B-A)*P(11)
2030 DELETE P
2040 RETURN 
2050 I9=0
2060 GO TO 2020
2070 I9=1
2080 GO TO 2020
2090 REM  END OF TRAP. INTEGRATION 
2100 REM  ***********************************************************
2110 REM 




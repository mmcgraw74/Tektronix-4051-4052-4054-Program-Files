1 END 
200 REM  REAL HYPERBOLIC TRIG. FUNCTIONS
205 REM  REFN:  A. ABRAMOWITZ AND I.A. STEGUN (EDITORS),
210 REM         "HANDBOOK OF MATHEMATICAL FUNCTIONS", AMS 55, 
215 REM         DEPT. OF COMMERCE, WASHINGTON, D.C., 1964 
220 REM  PARAMETERS 
225 REM    X - USER SUPPLIED VALUE
230 REM    Y - VALUE RETURNED TO USER 
235 REM 
250 REM  Y = SINH(X) = (EXP(X)-1/EXP(X))*.5 
255 Y=EXP(X)
260 Y=0.5*(Y-1/Y)
265 RETURN 
270 REM  END SINH *** 
275 REM 
300 REM  Y = COSH(X) = (EXP(X)+1/EXP(X))*.5 
305 Y=EXP(X)
310 Y=0.5*(Y+1/Y)
315 RETURN 
320 REM  END COSH *** 
325 REM 
350 REM  Y = TANH(X) = (EXP(X)-1/EXP(X))/(EXP(X)+EXP(X))
355 Y=EXP(X)
360 Y=(Y-1/Y)/(Y+1/Y)
365 RETURN 
370 REM  END TANH *** 
375 REM 
400 REM  Y = ARCSINH(X) = LN(X+SQRT(X^2+1)) 
405 Y=LOG(X+SQR(X^2+1))
410 RETURN 
415 REM  END ARCSINH ***
420 REM 
450 REM  Y = ARCCOSH(X) = LN(X+SQRT(X^2-1))  FOR X >= 1 
455 IF X=>1 THEN 470
460 PRINT "ERROR IN ARCCOSH: X<1, X = ";X
465 END 
470 Y=LOG(X+SQR(X^2-1))
475 RETURN 
480 REM  END ARCCOSH ***
485 REM 
500 REM  Y = ARCTANH(X) = .5 * LN((1+X)/(1-X))  FOR ABS(X)<1
505 IF ABS(X)<1 THEN 520
510 PRINT "ERROR IN ARCTANH: ABS >= 1, X = ";X
515 END 
520 Y=0.5*LOG((1+X)/(1-X))
525 RETURN 
530 REM  END ARCTANH ***
535 REM 




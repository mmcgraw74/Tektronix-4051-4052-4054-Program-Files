190 END 
200 REM  COMPLEX TRIG FUNCTIONS (REGULAR AND HYPERBOLIC)  **********
205 REM  REFN:  A. ABRAMOWITZ AND I.A. STEGUN (EDITORS),
210 REM         "HANDBOOK OF MATHEMATICAL FUNCTIONS", AMS 55, 
215 REM  DEPT. OF COMMERCE, WASHINGTON, D.C., 1964. 
220 REM  Z = (X,Y) IN RECTANGULAR COORDINATES 
225 REM  Z2 = SIN(Z1), SINH(Z1), ETC. 
230 REM 
250 REM  Z2 = SIN(Z1)  ********** 
255 Y2=EXP(Y1)
260 X2=0.5*SIN(X1)*(Y2+1/Y2)
265 Y2=0.5*COS(X1)*(Y2-1/Y2)
270 RETURN 
275 REM   END SIN  ********** 
280 REM 
300 REM  Z2 = SINH(Z1) = I*SIN(-I*Z1)  ********** 
305 Y2=EXP(-X1)
310 X2=-0.5*COS(Y1)*(Y2-1/Y2)
315 Y2=0.5*SIN(Y1)*(Y2+1/Y2)
320 RETURN 
325 REM   END SINH  *********** 
330 REM 
350 REM  Z2 = COS(Z1)  ************ 
355 Y2=EXP(Y1)
360 X2=0.5*COS(X1)*(1/Y2+Y2)
365 Y2=0.5*SIN(X1)*(1/Y2-Y2)
370 RETURN 
375 REM   END COS  ***********
380 REM 
400 REM  Z2 = COSH(Z1) = COS(I*Z1)  **********
405 Y2=EXP(X1)
410 X2=0.5*COS(Y1)*(1/Y2+Y2)
415 Y2=-0.5*SIN(Y1)*(1/Y2-Y2)
420 RETURN 
425 REM   END COSH  *********** 
430 REM 
450 REM  Z2 = TAN(Z1)  ************** 
455 P8=EXP(2*Y1)
460 P9=1/P8
465 X2=(P8+P9)*0.5+COS(2*X1)
467 IF ABS(X2)>1.0E-12 THEN 470
468 PRINT "ERROR IN TAN Z -- TAN Z IS INFINITE, Z = ";X1;Y1
469 END 
470 Y2=(P8-P9)*0.5/X2
475 X2=SIN(2*X1)/X2
480 RETURN 
485 REM   END TAN  ********** 
490 REM 
500 REM  Z2 = TANH(Z1) = -I*TAN(I*Z1)  ********** 
505 P8=EXP(2*X1)
510 P9=1/P8
515 Y2=(P8+P9)*0.5+COS(2*Y1)
517 IF ABS(Y2)>1.0E-12 THEN 520
518 PRINT "ERROR IN TANH Z -- TANH Z IS INIFINITE, Z = ";X1;Y1
519 END 
520 X2=(P8-P9)*0.5/Y2
525 Y2=SIN(2*Y1)/Y2
530 RETURN 
535 REM   END TANH  *********** 
540 REM 




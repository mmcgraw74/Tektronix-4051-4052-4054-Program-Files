1000 REM  *************RESIDENT OVERLAY***************************
1010 GO TO P9 OF 1100,1450,1400,1840,1630,1880,1990,1740,2160
1020 IF P9<46 THEN 1050
1030 PRINT "
DRAW A GRAPH BEFORE REQUESTING AN AXIS"
1040 END 
1050 GOSUB 2400
1060 IF P9<20 THEN 100
1070 P9=P9-30
1080 GO TO 100
1090 REM  *****INITIALIZE***************
1100 DELETE X,Y,P,C1,C2
1110 SET KEY 
1120 PAGE 
1130 PRINT "
PLANAR CURVE FIT BY SPLINES UNDER TENSION
"
1140 PRINT "ENTER AN UPPER BOUND ON THE NUMBER OF DATA PAIRS = ";
1150 INPUT P1
1160 PRINT 
1170 IF P1<3 THEN 1140
1180 DIM X(P1),Y(P1),P(22)
1190 P(1)=INT(P1)
1200 Q9=32
1210 Q0=100
1220 P(13)=1
1230 N=0
1240 P(12)=-P9
1250 P1=1
1260 PRINT "
PRESS ""FROM TAPE"" {KEY #4} NOW TO ENTER DATA FROM TAPE."
1270 REM  ********ENTER (XI,YI)*******************************
1280 PRINT 
1290 PRINT P1;"  ENTER X,Y = ";
1300 INPUT X(P1),Y(P1)
1310 N=P1
1320 P1=N+1
1330 IF N<P(1) THEN 1280
1340 PRINT 
1350 PRINT N;" DATA PAIRS ENTERED - THE MAXIMUM REQUESTED"
1360 END 
1370 REM  *****END DATA ENTRY*****
1380 REM
1390 REM  *****DELETE LAST POINT***** 
1400 P1=N
1410 N=N-1
1420 GO TO 1280
1430 REM
1440 REM  *****DELETE DATA POINT***** 
1450 PRINT "
DELETE DATA POINTS"
1460 PRINT 
1470 PRINT "ENTER POINT NUMBER = ";
1480 INPUT P4
1490 P4=INT(P4)
1500 IF P4>N OR P4<=0 THEN 1460
1510 IF P4=N THEN 1580
1520 P6=P4
1530 FOR P5=P4+1 TO N
1540 X(P6)=X(P5)
1550 Y(P6)=Y(P5)
1560 P6=P5
1570 NEXT P5
1580 N=N-1
1590 GO TO 1460
1600 REM  *****END OF DELETES*****
1610 REM
1620 REM  *****CORRECT DATA*****
1630 PRINT "
CORRECT THE DATA"
1640 PRINT 
1650 PRINT "ENTER POINT NUMBER = ";
1660 INPUT P4
1670 PRINT "PRESENT VALUES   = ";X(P4),Y(P4)
1680 PRINT "ENTER NEW VALUES = ";
1690 INPUT X(P4),Y(P4)
1700 GO TO 1640
1710 REM  *****END OF CORRECT DATA***** 
1720 REM
1730 REM  *****PRINT THE DATA*****
1740 PAGE 
1750 PRINT " I	XI	YI "
1760 FOR P4=1 TO N
1770 PRINT 
1780 PRINT P4,X(P4),Y(P4)
1790 NEXT P4
1800 END 
1810 REM  *****END OF PRINT*****
1820 REM
1830 REM  *****RESUME ENTRY*****
1840 GO TO 1320
1850 REM  *****END OF RESUME ENTRY***** 
1860 REM
1870 REM  *****FROM TAPE***** 
1880 Q$="FROM"
1890 GOSUB 2080
1900 READ @P3:N
1910 FOR P4=1 TO N
1920 READ @P3:X(P4),Y(P4)
1930 NEXT P4
1940 PRINT "
PUT PROGRAM TAPE BACK IN."
1950 END 
1960 REM  *****END ENTRY FROM TAPE***** 
1970 REM
1980 REM  *****TO TAPE***** 
1990 Q$="TO"
2000 GOSUB 2080
2010 WRITE @P3:N
2020 FOR P4=1 TO N
2030 WRITE @P3:X(P4),Y(P4)
2040 NEXT P4
2050 GO TO 1940
2060 REM  *****END STORE ONTO TAPE*****
2070 REM
2080 PRINT "DATA ";Q$;" TAPE"
2090 P3=33
2100 PRINT "
INSERT DATA TAPE AND ENTER THE TAPE FILE NUMBER = ";
2110 INPUT P2
2120 FIND P2
2130 RETURN 
2140 REM  ****************************************************
2150 REM  ***************INSERT DATA**************************
2160 PRINT 
2170 PRINT "INSERT DATA POINTS"
2180 PRINT 
2190 IF N=P(1) THEN 1340
2200 PRINT "  BEFORE POINT NUMBER = ";
2210 INPUT P1
2220 P1=INT(P1)
2230 IF P1<1 THEN 2180
2240 IF P1>N THEN 2180
2250 PRINT "        POINT NUMBER ";P1;"   = ";X(P1),Y(P1)
2260 PRINT "        ENTER NEW POINT = ";
2270 INPUT P6,P7
2280 P3=N+1
2290 FOR P2=N TO P1 STEP -1
2300 X(P3)=X(P2)
2310 Y(P3)=Y(P2)
2320 P3=P2
2330 NEXT P2
2340 N=N+1
2350 X(P1)=P6
2360 Y(P1)=P7
2370 GO TO 2180
2380 REM  ********************************************************
2390 REM  *******CHECK THAT SUCCESSIVE POINTS ARE DISTINCT********
2400 P2=1
2410 P3=0
2420 IF N>1 THEN 2450
2430 PRINT "ENTER MORE DATA POINTS - MIN. OF 2 REQUIRED"
2440 GO TO 1280
2450 FOR P1=2 TO N
2460 IF X(P1)<>X(P2) THEN 2510
2470 IF Y(P1)<>Y(P2) THEN 2510
2480 P3=P1
2490 PRINT 
2500 PRINT "POINT #";P2;" IS EQUAL TO #";P1;"   = ";X(P1),Y(P1)
2510 P2=P1
2520 NEXT P1
2530 IF P3=0 THEN 2570
2540 PRINT 
2550 PRINT "DELETE THE EXTRA POINTS AND TRY AGAIN."
2560 END 
2570 RETURN 
2580 REM  ***************************************************** 
2590 REM  ************END OF OLV1****************************** 
2600 REM  ***************************************************** 
2610 REM  



1 PRINT "REAL SYMMETRIC LINEAR EQUATIONS
"
2 PRINT "PRESS  KEY #1 (INITIALIZE)   OR   KEY #4 (FROM TAPE)

"
3 END 
4 GOSUB 1200
8 END 
12 END 
16 GOSUB 3080
20 END 
24 GOSUB 2080
28 END 
32 END 
36 END 
40 END 
44 GO TO 1570
48 END 
52 END 
56 GOSUB 3500
60 END 
64 GOSUB 1830
68 END 
72 END 
76 END 
80 END 
1000 REM****************************** 
1010 REM SOLUTION OF EQUATION AX=B WHERE A IS A REAL SYMMETRIC MATRIX
1020 REM  PROVIDES ALSO THE DETERMINANT AND INVERSE OF MATRIX A
1030 REM 
1040 REM REFN:  ARGONNE NATIONAL LABS, ANL F152S, SYMMETRIC MATRIX
1050 REM   INVERSION WITH ACCOMPANYING SOLUTION OF LINEAR EQUATIONS, 
1060 REM   BURTON S. GARBOW, JUNE,1968.
1070 REM 
1080 REM PARAMETERS: 
1090 REM   INPUT:
1100 REM     A - REAL SYMMETRIC MATRIX
1110 REM     B - CONSTANT VECTORS OF LENGTH N
1120 REM     N - ROW DIM. OF A & B - THE NUMBER OF LINEAR EQUATIONS
1130 REM     M - NUMBER OF COLUMN VECTORS OF B
1140 REM   OUTPUT:
1150 REM     D - DETERMINANT VALUE OF A
1160 REM     A - INVERSE MATRIX OF A
1170 REM     B - SOLUTION VECTORS
1180 REM****************************** 
1190 REM ENTER MATRIX ROUTINE
1200 SET NOKEY 
1210 PRINT "	    SYSTEM OF LINEAR EQUATIONS"
1220 PRINT " 	REAL SYMMETRIC COEFFICIENT MATRIX"
1230 PRINT "ENTER THE # OF LINEAR EQUATIONS (ROWS OF MATRIX) = ";
1240 INPUT N
1250 N=INT(N)
1260 PRINT "ENTER THE # OF CONSTANT VECTOR(S) YOU ARE ENTERING = ";
1270 INPUT M
1280 M=INT(M)
1290 Q1=0.5*N*(N+1)
1300 DELETE A,B,P$
1310 DIM A(Q1),B(N,M MAX 1),P$(1)
1320 PRINT "ENTER THE ENTIRE MATRIX BEFORE USING ANY OTHER KEYS."
1330 P4=1
1340 PRINT "ENTER UPPER TRIANGULAR ELEMENTS OF COEFFICIENT MATRIX A"
1350 FOR P1=1 TO N
1360 PRINT "ROW ";P1
1370 FOR P2=P1 TO N
1380 PRINT "A(";P1;",";P2;") = ";
1390 INPUT A(P4)
1400 P4=P4+1
1410 NEXT P2
1420 NEXT P1
1430 IF M<=0 THEN 1520
1440 PRINT "ENTER THE ELEMENTS OF CONSTANT MATRIX B"
1450 FOR P1=1 TO N
1460 PRINT "ROW ";P1
1470 FOR P2=1 TO M
1480 PRINT "B(";P1;",";P2;") = ";
1490 INPUT B(P1,P2)
1500 NEXT P2
1510 NEXT P1
1520 SET KEY 
1530 PRINT "ENTRY COMPLETE"
1540 RETURN 
1550 REM****************************** 
1560 REM CORRECT MATRIX ROUTINE
1570 PRINT "CORRECT THE MATRIX: ENTER ROW AND COLUMN"
1580 PRINT "OF THE ELEMENT TO BE CORRECTED"
1590 PRINT "CORRECT MATRIX A (YES-A, NO-B): ";
1600 INPUT P$
1610 IF P$="N" THEN 1710
1620 IF P$<>"Y" THEN 1590
1630 PRINT "(A MATRIX) ROW,COLUMN = ";
1640 INPUT P1,P2
1650 IF P2<P1 THEN 1770
1660 Q3=Q1-0.5*(N-P1+1)*(N-P1+2)+P2-P1+1
1670 PRINT "PRESENT VALUE   = ";A(Q3)
1680 PRINT "ENTER NEW VALUE = ";
1690 INPUT A(Q3)
1700 GO TO 1630
1710 PRINT "(B MATRIX) ROW,COLUMN = ";
1720 INPUT P1,P2
1730 PRINT "PRESENT VALUE   = ";B(P1,P2)
1740 PRINT "ENTER NEW VALUE = ";
1750 INPUT B(P1,P2)
1760 GO TO 1710
1770 P3=P1
1780 P1=P2
1790 P2=P3
1800 GO TO 1660
1810 REM****************************** 
1820 REM PRINT MATRIX ROUTINE
1830 PRINT "	 *A* MATRIX (UPPER TRIANGULAR PORTION)"
1840 P4=1
1850 FOR P1=1 TO N
1860 PRINT "ROW ";P1
1870 IF P1=1 THEN 1910
1880 FOR P3=1 TO P1-1
1890 PRINT "	";
1900 NEXT P3
1910 FOR P2=P1 TO N
1920 PRINT A(P4),"";
1930 P4=P4+1
1940 NEXT P2
1950 NEXT P1
1960 IF M<=0 THEN 2040
1970 PRINT "	*B* MATRIX"
1980 FOR P1=1 TO N
1990 PRINT "ROW ";P1
2000 FOR P3=1 TO M
2010 PRINT B(P1,P3),"";
2020 NEXT P3
2030 NEXT P1
2040 PRINT 
2050 RETURN 
2060 REM****************************** 
2070 REM SOLVE SYSTEM ROUTINE
2080 PAGE 
2090 DELETE P,Q,Q2,P9
2100 DIM P(N),Q(N),Q2(M MAX 1),P9(N)
2110 REM INITIALIZATION
2120 D=1
2130 P9=0
2140 REM GRAND LOOP
2150 FOR P3=1 TO N
2160 REM SEARCH FOR PIVOT ELEMENT
2170 P5=0
2180 Q4=N+1
2190 Q3=1
2200 FOR P1=1 TO N
2210 IF P9(P1)<>0 THEN 2260
2220 P4=ABS(A(Q3))
2230 IF P4<P5 THEN 2260
2240 P7=P1
2250 P5=P4
2260 Q3=Q3+Q4-P1
2270 NEXT P1
2280 P1=P7
2290 P2=P1
2300 Q3=Q1-(N-P1+1)*(N-P1+2)/2+1
2310 P5=A(Q3)
2320 D=P5*D
2330 REM IF PIVOT ELEMENT IS ZERO, PROGRAM FAILS 
2340 IF D=0 THEN 3030
2350 REM PREPARATION OF ELIMINATION STEP 
2360 P9(P7)=P5
2370 P(P7)=1
2380 Q(P7)=P(P7)/P5
2390 A(Q3)=0
2400 IF M<=0 THEN 2460
2410 FOR P2=1 TO M
2420 Q2(P2)=B(P1,P2)
2430 B(P1,P2)=0
2440 NEXT P2
2450 REM 
2460 IF P7=1 THEN 2590
2470 P6=P7-1
2480 P2=P7
2490 Q3=P7
2500 FOR P1=1 TO P6
2510 P(P1)=A(Q3)
2520 Q(P1)=P(P1)*Q(P7)
2530 IF P9(P1)<>0 THEN 2550
2540 Q(P1)=-Q(P1)
2550 A(Q3)=0
2560 Q3=Q3+N-P1
2570 NEXT P1
2580 REM 
2590 IF P7=N THEN 2720
2600 P6=P7+1
2610 P1=P7
2620 Q3=Q1-(N-P7+1)*(N-P7+2)/2+2
2630 FOR P2=P6 TO N
2640 P(P2)=-A(Q3)
2650 Q(P2)=P(P2)*Q(P7)
2660 IF P9(P2)<>0 THEN 2680
2670 P(P2)=-P(P2)
2680 A(Q3)=0
2690 Q3=Q3+1
2700 NEXT P2
2710 REM ELIMINATION PROPER
2720 Q3=0
2730 FOR P1=1 TO N
2740 FOR P2=P1 TO N
2750 Q3=Q3+1
2760 A(Q3)=A(Q3)+P(P1)*Q(P2)
2770 NEXT P2
2780 IF M<=0 THEN 2820
2790 FOR P2=1 TO M
2800 B(P1,P2)=B(P1,P2)+Q(P1)*Q2(P2)
2810 NEXT P2
2820 NEXT P1
2830 NEXT P3
2840 PRINT "DET(A) = ";D
2850 IF M<=0 THEN 2930
2860 PRINT "THE SOLUTION VECTOR(S) X:"
2870 FOR P1=1 TO M
2880 PRINT "VECTOR ";P1
2890 FOR P2=1 TO N
2900 PRINT B(P2,P1)
2910 NEXT P2
2920 NEXT P1
2930 DELETE P,Q,Q2,P9
2940 PRINT "DO YOU WANT TO SEE THE INVERSE OF MATRIX A: ";
2950 INPUT P$
2960 IF P$="N" THEN 3020
2970 IF P$<>"Y" THEN 2930
2980 P8=M
2990 M=0
3000 GOSUB 1840
3010 M=P8
3020 RETURN 
3030 PRINT "YOUR EQUATIONS ARE NOT LINEARLY INDEPENDENT; THEREFORE,"
3040 PRINT "NO UNIQUE SOLUTION EXISTS AND NO INVERSE MATRIX EXISTS."
3050 RETURN 
3060 REM ******************************
3070 REM DATA FROM TAPE
3080 P2=33
3090 PRINT "	    SYSTEM OF LINEAR EQUATIONS"
3100 PRINT "	REAL SYMMETRIC COEFFICIENT MATRIX"
3110 PRINT "DATA FROM TAPE"
3120 DELETE Q$,P$,A,B
3130 DIM Q$(24),P$(1)
3140 Q$="REAL SYMMETRIC"
3150 GOSUB 3680
3160 READ @P2:N
3170 P5=0.5*N*(N+1)
3180 DIM A(P5)
3190 READ @P2:A
3200 PRINT "DO YOU WISH TO ENTER CONSTANT VECTOR(S) FROM TAPE: ";
3210 INPUT P$
3220 IF P$="Y" THEN 3300
3230 IF P$<>"N" THEN 3200
3240 PRINT "DO YOU WISH TO ENTER CONSTANT VECTOR(S) FROM KEYBOARD: ";
3250 INPUT P$
3260 IF P$="Y" THEN 3380
3270 IF P$<>"N" THEN 3240
3280 M=0
3290 GO TO 3360
3300 Q$="CONSTANT VECTOR"
3310 GOSUB 3680
3320 READ @P2:P4,M
3330 IF P4<>N THEN 3450
3340 DIM B(N,M)
3350 READ @P2:B
3360 PRINT "ENTRY FROM TAPE COMPLETE"
3370 RETURN 
3380 SET NOKEY 
3390 PRINT "ENTER THE NUMBER OF CONSTANT VECTOR(S) YOU ARE ENTERING = ";
3400 INPUT M
3410 M=INT(M)
3420 DELETE B
3430 DIM B(N,M)
3440 GO TO 1440
3450 PRINT "CONSTANT VECTOR(S) ARE OF DIFFERENT LENGTH THAN THE"
3460 PRINT "COEFFICIENT MATRIX, CHECK THE FILE NO."
3470 GO TO 3310
3480 REM ******************************
3490 REM DATA TO TAPE
3500 P2=33
3510 PRINT "DATA TO TAPE"
3520 DELETE Q$,P$
3530 DIM Q$(24),P$(1)
3540 Q$="REAL SYMMETRIC"
3550 GOSUB 3680
3560 WRITE @P2:N,A
3570 IF M<=0 THEN 3620
3580 PRINT "DO YOU WISH TO WRITE CONSTANT/SOLN. VECTOR(S) TO TAPE: ";
3590 INPUT P$
3600 IF P$="Y" THEN 3630
3610 IF P$<>"N" THEN 3580
3620 GO TO 3660
3630 Q$="CONSTANT/SOLUTION VECTOR"
3640 GOSUB 3680
3650 WRITE @P2:N,M,B
3660 PRINT "DATA TO TAPE COMPLETE"
3670 RETURN 
3680 PRINT "ENTER THE FILE NUMBER OF THE ";Q$;" MATRIX = ";
3690 INPUT P3
3700 FIND P3
3710 RETURN 




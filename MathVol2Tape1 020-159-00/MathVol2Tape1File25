1 PRINT "COMPLEX LINEAR EQUATIONS
"
2 PRINT "PRESS  KEY #1 (INITIALIZE)   OR   KEY #4 (FROM TAPE)

"
3 END 
4 GOSUB 1240
8 END 
12 END 
16 GOSUB 3270
20 END 
24 GOSUB 1980
28 END 
32 END 
36 END 
40 END 
44 GO TO 1600
48 END 
52 END 
56 GOSUB 3780
60 END 
64 GOSUB 1800
68 END 
72 END 
76 END 
80 END 
1000 REM****************************** 
1010 REM SOLUTION OF EQUATION AX=B (A IS COMPLEX COEFFICIENT MATRIX)
1020 REM   PROVIDES ALSO THE DETERMINATE AND INVERSE OF MATRIX A 
1030 REM 
1040 REM REFN: ARGONNE NATIONAL LABORATORY, ANL F453S, COMPLEX MATRIX
1050 REM   INVERSION WITH ACCOMPANYING SOLUTION OF LINEAR EQUATIONS
1060 REM   BURTON S. GARBOW, FEBRUARY,1968.
1070 REM 
1080 REM PARAMETERS: 
1090 REM   INPUT:
1100 REM     A=(A1,A2)     - COMPLEX COEFFICIENTS ARE AS FOLLOWS:
1110 REM                     (A=A1+A2i) A1,A2 ARE SQUARE MATRICES WITH
1120 REM                     REAL NUMBER COEFFICIENTS
1130 REM     B=(B1,B2)     - CONSTANT VECTORS OF LENGTH N
1140 REM                     (B=B1+B2i) B1,B2 CONTAIN REAL NUMBER
1150 REM                     COEFFICIENTS
1160 REM     N             - ROW DIM. OF A & B - THE # OF LINEAR EQS.
1170 REM     M             - NUMBER OF COLUMN VECTORS OF B
1180 REM   OUTPUT:
1190 REM     D=(D(1),D(2)) - DETERMINANT OF MATRIX A (D=D(1)+D(2)i)
1200 REM     A=(A1,A2)     - INVERSE MATRIX OF A (A=A1+A2i)
1210 REM     B=(B1,B2)     - SOLUTION VECTORS (B=B1+B2i)
1220 REM****************************** 
1230 REM ENTER MATRIX ROUTINE
1240 SET NOKEY 
1250 PRINT "	SYSTEM OF LINEAR EQUATIONS"
1260 PRINT "	COMPLEX COEFFICIENT MATRIX"
1270 PRINT "ENTER THE # OF LINEAR EQUATIONS (ROWS OF MATRIX) = ";
1280 INPUT N
1290 N=INT(N)
1300 PRINT "ENTER THE # OF CONSTANT VECTOR(S) YOU ARE ENTERING = ";
1310 INPUT M
1320 M=INT(M)
1330 DELETE A1,A2,B1,B2,P$
1340 DIM A1(N,N),A2(N,N),B1(N,M MAX 1),B2(N,M MAX 1),P$(1)
1350 PRINT "ENTER THE ENTIRE MATRIX BEFORE USING ANY OTHER KEYS."
1360 PRINT "ENTER THE COMPLEX VALUES AS REAL NUMBER PAIRS X,Y"
1370 PRINT "BOTH ENTRIES ARE ALWAYS REQUIRED"
1380 PRINT "ENTER THE ELEMENTS OF COEFFICIENT MATRIX A"
1390 FOR P1=1 TO N
1400 PRINT "COLUMN ";P1
1410 FOR P2=1 TO N
1420 PRINT "A(";P2;",";P1;") = ";
1430 INPUT A1(P2,P1),A2(P2,P1)
1440 NEXT P2
1450 NEXT P1
1460 IF M<=0 THEN 1550
1470 PRINT "ENTER THE ELEMENTS OF CONSTANT MATRIX B"
1480 FOR P1=1 TO M
1490 PRINT "COLUMN ";P1
1500 FOR P2=1 TO N
1510 PRINT "B(";P2;",";P1;") = ";
1520 INPUT B1(P2,P1),B2(P2,P1)
1530 NEXT P2
1540 NEXT P1
1550 SET KEY 
1560 PRINT "ENTRY COMPLETE"
1570 RETURN 
1580 REM****************************** 
1590 REM CORRECT MATRIX ROUTINE
1600 PRINT "CORRECT THE MATRIX: ENTER ROW AND COLUMN"
1610 PRINT "OF THE ELEMENT TO BE CORRECTED"
1620 PRINT "CORRECT MATRIX A (YES-A, NO-B): ";
1630 INPUT P$
1640 IF P$="N" THEN 1720
1650 IF P$<>"Y" THEN 1620
1660 PRINT "(A MATRIX) ROW,COLUMN = ";
1670 INPUT P1,P2
1680 PRINT "PRESENT VALUE   = (";A1(P1,P2);",";A2(P1,P2);")"
1690 PRINT "ENTER NEW VALUE = ";
1700 INPUT A1(P1,P2),A2(P1,P2)
1710 GO TO 1660
1720 PRINT "(B MATRIX) ROW,COLUMN = ";
1730 INPUT P1,P2
1740 PRINT "PRESENT VALUE   = (";B1(P1,P2);",";B2(P1,P2);")"
1750 PRINT "ENTER NEW VALUE = ";
1760 INPUT B1(P1,P2),B2(P1,P2)
1770 GO TO 1720
1780 REM****************************** 
1790 REM PRINT MATRIX ROUTINE
1800 PRINT "*A* MATRIX"
1810 FOR P1=1 TO N
1820 PRINT "COLUMN ";P1
1830 FOR P2=1 TO N
1840 PRINT "(";A1(P2,P1);",";A2(P2,P1);")"
1850 NEXT P2
1860 NEXT P1
1870 IF M<=0 THEN 1950
1880 PRINT "*B* MATRIX"
1890 FOR P1=1 TO M
1900 PRINT "COLUMN ";P1
1910 FOR P2=1 TO N
1920 PRINT "(";B1(P2,P1);",";B2(P2,P1);")"
1930 NEXT P2
1940 NEXT P1
1950 RETURN 
1960 REM****************************** 
1970 REM SOLVE SYSTEM ROUTINE
1980 PAGE 
1990 DELETE P8,P9,Q7,D
2000 DIM P9(N),P8(N),D(2),Q7(2)
2010 REM INITIALIZE DETERMINANT AND PIVOT ELEMENT ARRAY
2020 D(1)=1
2030 D(2)=0
2040 P9=0
2050 REM PERFORM SUCCESSIVE PIVOT OPERATIONS (GRAND LOOP)
2060 FOR P3=1 TO N
2070 REM SEARCH FOR PIVOT ELEMENT AND EXTEND DETERMINANT PRODUCT
2080 P5=0
2090 FOR P1=1 TO N
2100 IF P9(P1)<>0 THEN 2190
2110 FOR P2=1 TO N
2120 IF P9(P2)<>0 THEN 2180
2130 P4=A1(P1,P2)^2+A2(P1,P2)^2
2140 IF P4<P5 THEN 2180
2150 P6=P1
2160 P7=P2
2170 P5=P4
2180 NEXT P2
2190 NEXT P1
2200 P8(P3)=4096*P6+P7
2210 P1=P6
2220 Q7(1)=A1(P1,P7)
2230 Q7(2)=A2(P1,P7)
2240 Q1=D(1)
2250 D(1)=Q7(1)*D(1)-Q7(2)*D(2)
2260 D(2)=Q7(1)*D(2)+Q7(2)*Q1
2270 REM  CHECK FOR SINGULAR MATRIX
2280 IF D(1)*D(1)+D(2)*D(2)=0 THEN 3220
2290 P9(P7)=P5
2300 REM INTERCHANGE ROWS TO PUT PIVOT ELEMENT ON DIAGONAL 
2310 IF P6=P7 THEN 2520
2320 D(1)=-D(1)
2330 D(2)=-D(2)
2340 FOR P2=1 TO N
2350 Q1=A1(P1,P2)
2360 Q2=A2(P1,P2)
2370 A1(P1,P2)=A1(P7,P2)
2380 A2(P1,P2)=A2(P7,P2)
2390 A1(P7,P2)=Q1
2400 A2(P7,P2)=Q2
2410 NEXT P2
2420 IF M<=0 THEN 2520
2430 FOR P2=1 TO M
2440 Q1=B1(P1,P2)
2450 Q2=B2(P1,P2)
2460 B1(P1,P2)=B1(P7,P2)
2470 B2(P1,P2)=B2(P7,P2)
2480 B1(P7,P2)=Q1
2490 B2(P7,P2)=Q2
2500 NEXT P2
2510 REM DIVIDE PIVOT ROW BY PIVOT ELEMENT 
2520 Q1=Q7(1)
2530 Q2=Q7(2)
2540 Q8=1/(Q1*Q1+Q2*Q2)
2550 FOR P2=1 TO N
2560 IF P2<>P7 THEN 2590
2570 A1(P7,P2)=1
2580 A2(P7,P2)=0
2590 Q3=A1(P7,P2)
2600 A1(P7,P2)=(A1(P7,P2)*Q1+A2(P7,P2)*Q2)*Q8
2610 A2(P7,P2)=(A2(P7,P2)*Q1-Q3*Q2)*Q8
2620 NEXT P2
2630 IF M<=0 THEN 2700
2640 FOR P2=1 TO M
2650 Q3=B1(P7,P2)
2660 B1(P7,P2)=(B1(P7,P2)*Q1+B2(P7,P2)*Q2)*Q8
2670 B2(P7,P2)=(B2(P7,P2)*Q1-Q3*Q2)*Q8
2680 NEXT P2
2690 REM REDUCE NON-PIVOT ROWS 
2700 FOR P1=1 TO N
2710 IF P1=P7 THEN 2870
2720 Q1=A1(P1,P7)
2730 Q2=A2(P1,P7)
2740 A1(P1,P7)=0
2750 A2(P1,P7)=0
2760 FOR P2=1 TO N
2770 Q3=A1(P7,P2)
2780 A1(P1,P2)=A1(P1,P2)-(A1(P7,P2)*Q1-A2(P7,P2)*Q2)
2790 A2(P1,P2)=A2(P1,P2)-(Q3*Q2+A2(P7,P2)*Q1)
2800 NEXT P2
2810 IF M<=0 THEN 2870
2820 FOR P2=1 TO M
2830 Q3=B1(P7,P2)
2840 B1(P1,P2)=B1(P1,P2)-(B1(P7,P2)*Q1-B2(P7,P2)*Q2)
2850 B2(P1,P2)=B2(P1,P2)-(Q3*Q2+B2(P7,P2)*Q1)
2860 NEXT P2
2870 NEXT P1
2880 NEXT P3
2890 REM INTERCHANGE COLUMNS AFTER PERFORMING ALL PIVOT OPERATIONS
2900 FOR Q3=N TO 1 STEP -1
2910 P2=INT(P8(Q3)/4096)
2920 P7=P8(Q3)-4096*P2
2930 IF P2=P7 THEN 3020
2940 FOR P1=1 TO N
2950 Q1=A1(P1,P2)
2960 Q2=A2(P1,P2)
2970 A1(P1,P2)=A1(P1,P7)
2980 A2(P1,P2)=A2(P1,P7)
2990 A1(P1,P7)=Q1
3000 A2(P1,P7)=Q2
3010 NEXT P1
3020 NEXT Q3
3030 PRINT "DET(A) = (";D(1);",";D(2);")"
3040 IF M<=0 THEN 3120
3050 PRINT "THE SOLUTION VECTOR(S) X:"
3060 FOR P1=1 TO M
3070 PRINT "VECTOR ";P1
3080 FOR P2=1 TO N
3090 PRINT "(";B1(P2,P1);",";B2(P2,P1);")"
3100 NEXT P2
3110 NEXT P1
3120 DELETE P8,P9,D,Q7
3130 PRINT "DO YOU WANT TO SEE THE INVERSE OF MATRIX A: ";
3140 INPUT P$
3150 IF P$="N" THEN 3210
3160 IF P$<>"Y" THEN 3130
3170 Q6=M
3180 M=0
3190 GOSUB 1810
3200 M=Q6
3210 RETURN 
3220 PRINT "YOUR EQUATIONS ARE NOT LINEARLY INDEPENDENT; THEREFORE,"
3230 PRINT "NO UNIQUE SOLUTION EXISTS AND NO INVERSE MATRIX EXISTS."
3240 RETURN 
3250 REM ******************************
3260 REM DATA FROM TAPE
3270 P5=33
3280 PRINT "	SYSTEM OF LINEAR EQUATIONS"
3290 PRINT "	COMPLEX COEFFICIENT MATRIX"
3300 PRINT "DATA FROM TAPE"
3310 DELETE Q$,P$,A1,A2,B1,B2
3320 DIM Q$(24),P$(1)
3330 Q$="COMPLEX COEFFICIENT"
3340 GOSUB 4060
3350 READ @P5:N
3360 DIM A1(N,N),A2(N,N)
3370 FOR P1=1 TO N
3380 FOR P2=1 TO N
3390 READ @P5:A1(P1,P2),A2(P1,P2)
3400 NEXT P2
3410 NEXT P1
3420 PRINT "DO YOU WISH TO ENTER CONSTANT VECTOR(S) FROM TAPE: ";
3430 INPUT P$
3440 IF P$="Y" THEN 3520
3450 IF P$<>"N" THEN 3420
3460 PRINT "DO YOU WISH TO ENTER CONSTANT VECTOR(S) FROM KEYBOARD: ";
3470 INPUT P$
3480 IF P$="Y" THEN 3640
3490 IF P$<>"N" THEN 3460
3500 M=0
3510 GO TO 3620
3520 Q$="CONSTANT VECTOR"
3530 GOSUB 4060
3540 READ @P5:P4,M
3550 IF P4<>N THEN 3730
3560 DIM B1(N,M),B2(N,M)
3570 FOR P1=1 TO N
3580 FOR P2=1 TO M
3590 READ @P5:B1(P1,P2),B2(P1,P2)
3600 NEXT P2
3610 NEXT P1
3620 PRINT "ENTRY FROM TAPE COMPLETE"
3630 RETURN 
3640 SET NOKEY 
3650 PRINT "ENTER THE NUMBER OF CONSTANT VECTOR(S) YOU ARE ENTERING = ";
3660 INPUT M
3670 M=INT(M)
3680 DELETE B1,B2
3690 DIM B1(N,M),B2(N,M)
3700 PRINT "ENTER THE COMPLEX VALUES AS REAL NUMBER PAIRS X,Y"
3710 PRINT "BOTH ENTRIES ARE ALWAYS REQUIRED"
3720 GO TO 1470
3730 PRINT "CONSTANT VECTOR(S) ARE OF DIFFERENT LENGTH THAN"
3740 PRINT "THE COEFFICIENT MATRIX.  CHECK THE FILE NO."
3750 GO TO 3530
3760 REM ******************************
3770 REM DATA TO TAPE
3780 P5=33
3790 PRINT "DATA TO TAPE"
3800 DELETE Q$,P$
3810 DIM Q$(24),P$(1)
3820 Q$="COMPLEX COEFFICIENT"
3830 GOSUB 4060
3840 WRITE @P5:N
3850 FOR P1=1 TO N
3860 FOR P2=1 TO N
3870 WRITE @P5:A1(P1,P2),A2(P1,P2)
3880 NEXT P2
3890 NEXT P1
3900 IF M<=0 THEN 4050
3910 PRINT "DO YOU WISH TO WRITE CONSTANT/SOLN. VECTOR(S) TO TAPE: ";
3920 INPUT P$
3930 IF P$="Y" THEN 3960
3940 IF P$<>"N" THEN 3910
3950 GO TO 4040
3960 Q$="CONSTANT/SOLUTION VECTOR"
3970 GOSUB 4060
3980 WRITE @P5:N,M
3990 FOR P1=1 TO N
4000 FOR P2=1 TO M
4010 WRITE @P5:B1(P1,P2),B2(P1,P2)
4020 NEXT P2
4030 NEXT P1
4040 PRINT "DATA TO TAPE COMPLETE"
4050 RETURN 
4060 PRINT "ENTER THE FILE NUMBER OF THE ";Q$;" MATRIX = ";
4070 INPUT P3
4080 FIND P3
4090 RETURN 




1 PRINT "REAL LINEAR EQUATIONS BY LU DECOMPOSITION
"
2 PRINT "PRESS  KEY #1 (INITIALIZE)   OR   KEY #4 (FROM TAPE)

"
3 END 
4 GOSUB 1280
6 END 
8 END 
12 END 
16 GOSUB 3270
18 END 
20 END 
24 GOSUB 2390
26 END 
28 END 
32 END 
36 END 
40 END 
44 GO TO 1830
46 END 
48 END 
52 END 
56 GOSUB 3790
58 END 
60 END 
64 GOSUB 2150
66 END 
68 END 
72 END 
76 END 
80 END 
1000 REM******************************
1010 REM SOLUTION OF EQUATION AX=B WHERE A IS A SQUARE COEFFICIENT
1020 REM   MATRIX.  PROVIDES ALSO THE DETERMINATE OF MATRIX A
1030 REM  REFN*  MOLER, CLEVE B., CACM ALGORITHM 423, "LINEAR 
1040 REM         EQUATION SOLVER" 
1050 REM         FORSYTHE, G.E., AND MOLER, C.B.,  "COMPUTER
1060 REM         SOLUTION OF LINEAR ALGEBRAIC SYSTEMS"  1967
1070 REM         PRENTICE-HALL, ENGLEWOOD CLIFFS, N.J.
1080 REM
1090 REM  PARAMETERS
1100 REM   ****INPUT****
1110 REM     A - N BY N COEFFICIENT MATRIX
1120 REM     B - CONSTANT VECTOR, OF LENGTH N 
1130 REM     N - SIZE OF A AND B;  THE NUMBER OF LINEAR EQUATIONS 
1140 REM   ****OUTPUT**** 
1150 REM     A - LU DECOMPOSITION OF ORIGINIAL A
1160 REM     Q - PIVOT VECTOR OF LENGTH N:  WHEN SOLVING FOR AN 
1170 REM         UNKNOWN X, IT IS NEEDED ALONG WITH THE LU FORM OF A.
1180 REM     B - X SOLUTION VECTOR IN AX=B, (A,B ARE ORIGINAL A,B.)
1190 REM     D - DETERMINANT OF ORIGINAL A
1200 REM  P = 0 :  LU DECOMP. HAS NOT BEEN PERFORMED
1210 REM  P0 <> 0 :  ASK IF USER WANTS A NEW A MATRIX 
1220 REM  Q0 = 0  :  NO B VECTOR AVAILABLE
1230 REM  Q9 = 0  :  A NEW MATRIX WAS JUST ENTERED
1240 REM  ********************************* 
1250 REM  ********************************* 
1260 REM
1270 REM  ENTER MATRIX ROUTINE
1280 DIM P$(1)
1290 PRINT "
SOLUTION OF A SYSTEM OF REAL LINEAR EQUATIONS BY ";
1300 PRINT "LU DECOMPOSITION.
"
1310 P0=0
1320 IF P0=0 THEN 1370
1330 PRINT "ENTER A NEW MATRIX, A: ";
1340 INPUT P$
1350 IF P$="N" THEN 1660
1360 IF P$<>"Y" THEN 1330
1370 P=0
1380 Q0=0
1390 PRINT "
ENTER THE NUMBER OF LINEAR EQUATIONS (ROWS OF MATRIX) = ";
1400 INPUT N
1410 N=INT(N)
1420 PRINT "
ENTER THE ENTIRE MATRIX BEFORE USING ANY OTHER KEYS."
1430 DELETE A,Q
1440 DIM A(N,N),Q(N)
1450 SET NOKEY 
1460 PRINT 
1470 PRINT "ENTER THE ELEMENTS OF A"
1480 FOR P1=1 TO N
1490 PRINT "
ROW ";P1
1500 FOR P2=1 TO N
1510 PRINT "  A(";P1;",";P2;")  =  ";
1520 INPUT A(P1,P2)
1530 NEXT P2
1540 NEXT P1
1550 DELETE 1310
1560 DELETE 3320
1570 DELETE 3540,3570
1580 DELETE 1550,1580
1590 P0=1
1600 Q9=0
1610 PRINT 
1620 PRINT "ENTER A CONSTANT VECTOR, B: ";
1630 INPUT P$
1640 IF P$="N" THEN 1770
1650 IF P$<>"Y" THEN 1610
1660 IF Q9<>0 THEN 1700
1670 DELETE B
1680 DIM B(N)
1690 Q9=1
1700 SET NOKEY 
1710 PRINT 
1720 FOR P1=1 TO N
1730 PRINT "  B(";P1;")  =  ";
1740 INPUT B(P1)
1750 NEXT P1
1760 Q0=1
1770 PRINT 
1780 PRINT "ENTRY COMPLETE"
1790 RETURN 
1800 REM  ************************************* 
1810 REM  ************************************* 
1820 REM  CORRECT MATRIX OR VECTOR
1830 PRINT 
1840 SET KEY 
1850 PRINT "CORRECT THE MATRIX, A (YES-A, NO-B): ";
1860 INPUT P$
1870 PRINT 
1880 IF P$="N" THEN 2010
1890 IF P$<>"Y" THEN 1830
1900 IF P=0 THEN 1930
1910 PRINT "NO A MATRIX AVAILABLE -- DECOMP. HAS BEEN PERFORMED."
1920 END 
1930 PRINT "ENTER THE ROW AND COLUMN OF THE ELEMENT OF A"
1940 PRINT 
1950 PRINT "ROW, COLUMN = ";
1960 INPUT P1,P2
1970 PRINT "PRESENT VALUE   = ";A(P1,P2)
1980 PRINT "ENTER NEW VALUE = ";
1990 INPUT A(P1,P2)
2000 GO TO 1940
2010 IF Q0<>0 THEN 2040
2020 PRINT "NO B VECTOR IS PRESENT."
2030 END 
2040 PRINT "ENTER THE ROW OF THE ELEMENT OF B"
2050 PRINT 
2060 PRINT "ROW = ";
2070 INPUT P1
2080 PRINT "PRESENT VALUE   = ";B(P1)
2090 PRINT "ENTER NEW VALUE = ";
2100 INPUT B(P1)
2110 GO TO 2050
2120 REM  ************************************* 
2130 REM  ************************************* 
2140 REM  PRINT ROUTINE
2150 PAGE 
2160 IF P<>0 THEN 2190
2170 PRINT "A MATRIX"
2180 GO TO 2200
2190 PRINT "LU DECOMPOSITION OF A"
2200 FOR P1=1 TO N
2210 PRINT 
2220 PRINT "ROW ";P1
2230 FOR P2=1 TO N
2240 PRINT A(P1,P2),"";
2250 NEXT P2
2260 PRINT 
2270 NEXT P1
2280 IF Q0=0 THEN 2340
2290 PRINT 
2300 PRINT "
B VECTOR"
2310 FOR P1=1 TO N
2320 PRINT B(P1),"";
2330 NEXT P1
2340 PRINT 
2350 RETURN 
2360 REM  ************************************* 
2370 REM  ************************************* 
2380 REM  PERFORM LU DECOMPOSITON AND/OR SOLVE AX = B 
2390 PAGE 
2400 SET NOKEY 
2410 IF P<>0 THEN 2490
2420 GOSUB 2590
2430 D=Q(N)
2440 FOR P1=1 TO N
2450 D=D*A(P1,P1)
2460 NEXT P1
2470 PRINT 
2480 PRINT "DET(A)  =  ";D
2490 IF Q0=0 THEN 2560
2500 GOSUB 3000
2510 PRINT 
2520 PRINT "SOLUTION VECTOR, B"
2530 FOR P1=1 TO N
2540 PRINT B(P1),"";
2550 NEXT P1
2560 PRINT 
2570 RETURN 
2580 REM  **** CACM ALG. 423 -- LU DECOMPOSITION **** 
2590 Q(N)=1
2600 IF N<2 THEN 2890
2610 FOR P3=1 TO N-1
2620 P6=P3+1
2630 P4=P3
2640 FOR P1=P6 TO N
2650 IF ABS(A(P1,P3))<=ABS(A(P4,P3)) THEN 2670
2660 P4=P1
2670 NEXT P1
2680 Q(P3)=P4
2690 IF P4=P3 THEN 2710
2700 Q(N)=-Q(N)
2710 P5=A(P4,P3)
2720 A(P4,P3)=A(P3,P3)
2730 A(P3,P3)=P5
2740 IF P5=0 THEN 2930
2750 FOR P1=P6 TO N
2760 A(P1,P3)=-A(P1,P3)/P5
2770 NEXT P1
2780 FOR P2=P6 TO N
2790 P5=A(P4,P2)
2800 A(P4,P2)=A(P3,P2)
2810 A(P3,P2)=P5
2820 IF P5=0 THEN 2860
2830 FOR P1=P6 TO N
2840 A(P1,P2)=A(P1,P2)+A(P1,P3)*P5
2850 NEXT P1
2860 NEXT P2
2870 IF A(P3,P3)=0 THEN 2930
2880 NEXT P3
2890 IF A(N,N)=0 THEN 2930
2900 P=1
2910 RETURN 
2920 REM  PRINT ERROR MESSAGE 
2930 PRINT 
2940 PRINT "YOUR EQUATIONS ARE NOT LINEARLY INDEPENDENT."
2950 PRINT "THEREFORE NO UNIQUE SOLUTION EXISTS AND DET(A)  =  0"
2960 END 
2970 REM  END OF DECOMP 
2980 REM  ************************* 
2990 REM  SOLVE - FORWARD AND THEN BACKWARD SUBSTITUTION
3000 IF N=1 THEN 3200
3010 P2=N-1
3020 FOR P3=1 TO P2
3030 P6=P3+1
3040 P4=Q(P3)
3050 P5=B(P4)
3060 B(P4)=B(P3)
3070 B(P3)=P5
3080 FOR P1=P6 TO N
3090 B(P1)=B(P1)+A(P1,P3)*P5
3100 NEXT P1
3110 NEXT P3
3120 FOR P3=P2 TO 1 STEP -1
3130 P6=P3+1
3140 B(P6)=B(P6)/A(P6,P6)
3150 P5=-B(P6)
3160 FOR P1=1 TO P3
3170 B(P1)=B(P1)+A(P1,P6)*P5
3180 NEXT P1
3190 NEXT P3
3200 B(1)=B(1)/A(1,1)
3210 RETURN 
3220 REM  END OF SOLVE
3230 REM  ************************************* 
3240 REM  ************************************* 
3250 REM
3260 REM   DATA FROM TAPE 
3270 Q$="FROM"
3280 DIM P$(1)
3290 GOSUB 4100
3300 PRINT 
3310 PRINT "LOAD THE MATRIX, A";
3320 P0=0
3330 IF P0<>0 THEN 3360
3340 PRINT 
3350 GO TO 3400
3360 PRINT ": ";
3370 INPUT P$
3380 IF P$="N" THEN 3630
3390 IF P$<>"Y" THEN 3300
3400 GOSUB 4160
3410 P=0
3420 P0=1
3430 Q0=0
3440 Q9=0
3450 READ @P3:N
3460 DELETE A,Q
3470 IF N>0 THEN 3520
3480 N=-N
3490 P=1
3500 DIM Q(N)
3510 READ @P3:Q
3520 DIM A(N,N),Q(N)
3530 READ @P3:A
3540 DELETE 3320
3550 DELETE 1310
3560 DELETE 1550,1580
3570 DELETE 3540,3570
3580 PRINT 
3590 PRINT "LOAD A CONSTANT VECTOR, B: ";
3600 INPUT P$
3610 IF P$="N" THEN 3710
3620 IF P$<>"Y" THEN 3580
3630 IF Q9<>0 THEN 3670
3640 DELETE B
3650 DIM B(N)
3660 Q9=1
3670 GOSUB 4160
3680 READ @P3:P1
3690 READ @P3:B
3700 Q9=1
3710 PRINT 
3720 PRINT "DONE"
3730 RETURN 
3740 REM  END OF FROM TAPE
3750 REM  ************************************
3760 REM  ************************************
3770 REM
3780 REM  DATA TO TAPE
3790 Q$="TO"
3800 GOSUB 4100
3810 PRINT 
3820 PRINT "STORE THE MATRIX, A: ";
3830 INPUT P$
3840 IF P$="N" THEN 3980
3850 IF P$<>"Y" THEN 3810
3860 GOSUB 4160
3870 IF P=0 THEN 3910
3880 WRITE @P3:-N
3890 WRITE @P3:Q
3900 GO TO 3920
3910 WRITE @P3:N
3920 WRITE @P3:A
3930 PRINT 
3940 PRINT "STORE THE CONSTANT VECTOR, B: ";
3950 INPUT P$
3960 IF P$="N" THEN 4020
3970 IF P$<>"Y" THEN 3930
3980 IF Q0=0 THEN 2020
3990 GOSUB 4160
4000 WRITE @P3:N
4010 WRITE @P3:B
4020 PRINT 
4030 PRINT "DONE"
4040 RETURN 
4050 REM   END TO TAPE
4060 REM  ************************************* 
4070 REM  ************************************* 
4080 REM
4090 REM  TAPE UTILITY #1 
4100 PAGE 
4110 SET KEY 
4120 P3=33
4130 PRINT "DATA ";Q$;" TAPE"
4140 RETURN 
4150 REM  TAPE UTILITY #2 
4160 PRINT 
4170 PRINT "ENTER THE TAPE FILE NUMBER = ";
4180 INPUT P1
4190 FIND P1
4200 RETURN 
4210 REM  **************************************
4220 REM  **************************************


